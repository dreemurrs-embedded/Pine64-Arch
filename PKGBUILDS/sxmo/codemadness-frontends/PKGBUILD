# Maintainer: Anjandev Momi <anjan@momi.ca>
pkgname=codemadness-frontends
pkgver=0.5
pkgrel=1
pkgdesc="Experimental (cli, web, and gopher) frontends for youtube, reddit, & duckduckgo"
url="https://www.codemadness.org/idiotbox.html"
arch=('x86_64' 'armv7h' 'aarch64')
license=('ISC')
depends=('libretls')
source=(https://codemadness.org/releases/frontends/frontends-$pkgver.tar.gz)

prepare() {
  cd frontends-$pkgver
  sed -i 's/^#FRONTENDS_/FRONTENDS_/g' Makefile
  sed -i 's/^#LIBTLS_/LIBTLS_/g' Makefile
}

build() {
  cd frontends-$pkgver
  make \
    LIBTLS_LDFLAGS="$(pkg-config --libs libtls)" \
    LIBTLS_LDFLAGS_STATIC="$(pkg-config --libs --static libtls)" \
    CFLAGS="$CFLAGS $(pkg-config --cflags libtls)"
}

package() {
  cd frontends-$pkgver
  install -Dm755 youtube/cgi $pkgdir/usr/bin/youtube-cgi
  install -Dm755 youtube/gopher $pkgdir/usr/bin/youtube-gopher
  install -Dm755 youtube/cli $pkgdir/usr/bin/youtube-cli

  install -Dm755 duckduckgo/gopher $pkgdir/usr/bin/duckduckgo-gopher
  install -Dm755 duckduckgo/cli $pkgdir/usr/bin/duckduckgo-cli

  install -Dm755 reddit/gopher $pkgdir/usr/bin/reddit-gopher
  install -Dm755 reddit/cli $pkgdir/usr/bin/reddit-cli
}
sha256sums=('f0d2927f220c4b36964e022415d70f4d7fc409e4226ac64f7d91b516dfcb394c')
