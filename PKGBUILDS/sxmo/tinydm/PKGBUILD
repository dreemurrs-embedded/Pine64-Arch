# Maintainer: Aren <aren@peacevolution.org>

pkgname=tinydm
pkgver=1.1.2
pkgrel=3
pkgdesc="Tiny wayland / x11 session starter for single user machines"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.com/postmarketOS/tinydm"
license=('GPL3')
depends=('xorg-xinit' 'autologin')
source=("$pkgname-$pkgver.tar.bz2::https://gitlab.com/postmarketOS/tinydm/-/archive/$pkgver/tinydm-$pkgver.tar.bz2"
        "tinydm.service")
sha512sums=('81279224c062d01aa81b08804d9e753599f92d756ff2ef9012b585f44598a1f26122be653872e8c97dd8a1cbed7cc213f7f56ea284d370e5884506f01b1c1e0b'
            '577032bb608d594097770dc773884d7ab52dab9748cbcf658122bcb9b155c430ff183850a227d39f845726030aa47089fc63c08f9303b00d4425dbf54767af19')

prepare() {
  cd "$pkgname-$pkgver"

  # Remove logging setup, so logs go to stdout (and therefore the journal)
  sed -i 's@\(setup_log\)$@# \1@g' tinydm-run-session.sh
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm0644 "$srcdir/tinydm.service" "$pkgdir/usr/lib/systemd/system/tinydm.service"

  rm -r "$pkgdir/etc/init.d" 
  rm -r "$pkgdir/etc/conf.d" 
}
