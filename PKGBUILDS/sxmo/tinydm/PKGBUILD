# Maintainer: Aren <aren@peacevolution.org>

pkgname=tinydm
pkgver=1.1.2
pkgrel=2
pkgdesc="Tiny wayland / x11 session starter for single user machines"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.com/postmarketOS/tinydm"
license=('GPL3')
depends=('xorg-xinit')
source=("$pkgname-$pkgver.tar.bz2::https://gitlab.com/postmarketOS/tinydm/-/archive/$pkgver/tinydm-$pkgver.tar.bz2"
        "tinydm.service")
sha512sums=('81279224c062d01aa81b08804d9e753599f92d756ff2ef9012b585f44598a1f26122be653872e8c97dd8a1cbed7cc213f7f56ea284d370e5884506f01b1c1e0b'
            '5c67471bce07adfaec6927717aaf137aec1bbc92f659848f6d6714cd3ef84bbcf1a1c2369cef593b5194a50181127756d1554bb952f562c8cad3a881aeed46f2')

prepare() {
  cd "$pkgname-$pkgver"

  # Remove logging setup, so logs go to stdout (and therefore the journal)
  sed -i 's@\(setup_log\)$@# \1@g' tinydm-run-session.sh
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm0644 "$srcdir/tinydm.service" "$pkgdir/usr/lib/systemd/system/tinydm.service"

  rm -r "$pkgdir/etc/init.d" 
  rm -r "$pkgdir/etc/conf.d" 
}
