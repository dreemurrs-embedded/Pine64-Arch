# Maintainer: dni <office@dnilabs.com>
pkgname=sxmo-dwm
pkgver=6.2.15
pkgrel=1
pkgdesc="Dwm fork for Sxmo UI; supports volume-key hotkeys, swallow, keyboard, among other patches"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://git.sr.ht/~mil/sxmo-dwm"
license=('MIT')
depends=('xorg-server' 'freetype2' 'libx11' 'libxinerama' 'libxft')
provides=('dwm')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~mil/sxmo-dwm/archive/$pkgver.tar.gz")

prepare() {
  sed -i -e '/CFLAGS/{s/-Os//;s/=/+=/}' \
    -e '/LDFLAGS/{s/=/+=/}' "$pkgname-$pkgver/config.mk"
}

build() {
  cd "$pkgname-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

sha512sums=('a92eded8ca829b866f268d9d62980242a029c17febdf1a89d4944fc53f2da374e02e5a1255164aebaf6abf1d53278495e28fae38b2502328b814e27b849c5057')
