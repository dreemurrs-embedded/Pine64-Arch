# Maintainer: Aren <aren@peacevolution.org>
# Maintainer (aur): Martin Franc <me@martinfranc.eu>

pkgname=wob
pkgver=0.13
pkgrel=1
pkgdesc='A lightweight overlay volume/backlight/progress/anything bar for Wayland'
arch=('aarch64' 'i686' 'x86_64')
url='https://github.com/francma/wob'
license=('ISC')
depends=('wayland')
makedepends=('meson' 'wayland-protocols' 'scdoc')
source=(https://github.com/francma/wob/releases/download/${pkgver}/wob-${pkgver}.tar.gz{,.sig})
        # "${pkgname}-${pkgver}.tar.gz.sig::https://github.com/francma/wob/releases/download/${pkgver}/wob-${pkgver}.tar.gz.sig")
validpgpkeys=('5C6DA024DDE27178073EA103F4B432D5D67990E3')
sha512sums=('b4f15004b4e8a0176670d844969e29929e213dd787e3ad3bd04e805c07b3b7fa87721f79491f1205edaa7555fff8f9f2c7118873feac0f0a8a9d5eb9071d20f4'
            'SKIP')

build() {
  mkdir -p build
  arch-meson build "${pkgname}-${pkgver}" -D b_ndebug=true
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

check() {
  ninja -C build test
}
