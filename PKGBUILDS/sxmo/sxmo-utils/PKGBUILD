# Maintainer: dni <office@dnilabs.com>
pkgname=sxmo-utils
pkgver=1.5.3
pkgrel=2
pkgdesc="Utility scripts, programs, and configs that hold the Sxmo UI environment together"
url="https://git.sr.ht/~mil/sxmo-utils"
arch=('x86_64' 'armv7h' 'aarch64')
license=('MIT')
depends=('sxmo-dmenu' 'sxmo-dwm' 'sxmo-st' 'svkbd' 'lisgd' 'clickclack' 'mnc' 'pn' 'nerd-fonts-fira-mono'
         'bc' 'alsa-utils' 'autocutsel' 'bemenu' 'codemadness-frontends' 'conky' 'curl' 'dunst' 'ffmpeg'
         'nerd-fonts-terminus' 'geoclue' 'inotify-tools' 'light' 'mediainfo' 'modemmanager' 'mpv' 'ncurses'
         'networkmanager' 'sfeed' 'sxiv' 'terminus-font' 'unclutter' 'otf-latin-modern' 'ttf-dejavu'
         'v4l-utils' 'vis' 'w3m' 'xcalib' 'xclip' 'xdotool' 'xorg-xdpyinfo' 'xorg-xinput' 'xorg-xprop'
         'xorg-xrandr' 'xorg-xrdb' 'xsel' 'xorg-xset' 'xorg-xsetroot' 'xorg-xwininfo' 'youtube-dl'
         'busybox' 'bluez' 'bluez-utils' 'otf-latin-modern' 'libnotify')
makedepends=('libx11' 'xorgproto' 'linux-headers')
optdepends=('feh: set wallpaper')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~mil/sxmo-utils/archive/$pkgver.tar.gz"
        'rootfs-etc-NetworkManager-conf.d-00-sxmo.conf'
        'rootfs-etc-polkit-1-rules.d-00-sxmo.rules'
        'rootfs-etc-polkit-1-rules.d-50-org.freedesktop.NetworkManager.rules'
        'sxmo-setpermissions.service')

package() {
  cd "$pkgname-$pkgver"
  mkdir -p "$pkgdir/etc/modules-load.d/"
  printf %b "snd-aloop" > "$pkgdir/etc/modules-load.d/sxmo.conf"
  mkdir -p "$pkgdir/etc/modprobe.d/"
  printf %b "options snd slots=,snd-aloop" > "$pkgdir/etc/modprobe.d/sxmo.conf"

  make DESTDIR=$pkgdir install

  # Fix upgrade menu to use pacman
  sed -i 's@echo "Updating all packages from repositories"@@g' "$pkgdir/usr/bin/sxmo_upgrade.sh"
  sed -i 's@sudo apk update@@g' "$pkgdir/usr/bin/sxmo_upgrade.sh"
  sed -i 's@sudo apk upgrade@sudo pacman -Syu@g' "$pkgdir/usr/bin/sxmo_upgrade.sh"

  # Fix sxmo_timezonechange.sh to use arch command timedatectl
  sed -i 's@sudo setup-timezone -z@sudo timedatectl set-timezone@g' "$pkgdir/usr/bin/sxmo_timezonechange.sh"

  install -Dm644 "$srcdir/rootfs-etc-NetworkManager-conf.d-00-sxmo.conf" "$pkgdir/etc/NetworkManager/conf.d/00-sxmo.conf"
  install -Dm755 "$srcdir/rootfs-etc-polkit-1-rules.d-00-sxmo.rules" "$pkgdir/etc/polkit-1/rules.d/00-sxmo.rules"
  install -Dm755 "$srcdir/rootfs-etc-polkit-1-rules.d-50-org.freedesktop.NetworkManager.rules" "$pkgdir/etc/polkit-1/rules.d/50-org.freedesktop.NetworkManager.rules"

  # fix directory permissions
  chmod 750 "$pkgdir/etc/polkit-1/rules.d"
  chmod 750 "$pkgdir/etc/sudoers.d"

  install -Dm644 "$srcdir/sxmo-setpermissions.service" "$pkgdir/usr/lib/systemd/system/sxmo-setpermissions.service"
}

sha512sums=('02b02142368ac3bc0ccfd64b5c1d776b5a3e21c3ef21ae0958dcf691a497792d14ae576a75b63a29be531eb08ef5d9415bb1751ee85aef8ffb13b6f2e409deb4'
            '67a031f309a3232ac1e8abc3fedeaee912c035f9c81b4f709248895905a27ab5844ec92c65e55b79af3894450ba3883549d4004f11efebb47114d41f730e4a5f'
            '6e42f9acc015a21596cdf2d35eb5a80e8fbe97959aabf129861c8e1016bbebad5c706126730515494a0fcb33ad38fd6cd971048d2b5340557a803febf41967a4'
            '4c8f047c4608d89409a44db6370ca225d42e186323f42d0c564edb34f18c84a69a500a89cc2c31d5f0e5c292aa94ea20eaf8213e3e266b8f9e959c3d4ce9fb58'
            '532c3045f52f872704876bdf56afbc7470c5d17e46655dd4040d545b6d3eb34a8791909e554c560471f9494c7382107839bcff1e48be242d5eea0092482165cd')
