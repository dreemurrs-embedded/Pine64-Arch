# Maintainer: dni <office@dnilabs.com>
# Maintainer: Aren <aren@peacevolution.org>

pkgname=('sxmo-utils' 'sxmo-ui-sway-meta' 'sxmo-ui-dwm-meta')
pkgver=1.8.2
pkgrel=2
_pkgcommit=de71ed7b3400b96d59e4380466c0f9c39272e8d0
pkgdesc="Utility scripts, programs, and configs that hold the Sxmo UI environment together"
url="https://git.sr.ht/~mil/sxmo-utils"
arch=('x86_64' 'armv7h' 'aarch64')
license=('MIT')
makedepends=('libx11' 'xorgproto' 'linux-headers' 'busybox' 'git')
install=sxmo-utils.install
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~mil/sxmo-utils/archive/$_pkgcommit.tar.gz"
        '0001-Let-systemd-handle-service-supervision.patch'
        'rootfs-etc-NetworkManager-conf.d-00-sxmo.conf'
        'rootfs-etc-polkit-1-rules.d-00-sxmo.rules'
        'rootfs-etc-polkit-1-rules.d-50-org.freedesktop.NetworkManager.rules'
        'sxmo-setpermissions.service')
backup=('etc/doas.conf')

prepare() {
  cd "$pkgname-$_pkgcommit"

  # Use systemd to start & stop user services
  # TODO: this should be upstreamed
  sed -e 's/sxmo_daemons.sh start mmsd.*$/systemctl --user start mmsd-tng.service/g' \
      -e 's/sxmo_daemons.sh stop mmsd/systemctl --user stop mmsd-tng.service/g' \
      -i scripts/core/sxmo_mmsdconfig.sh
  sed -i 's/.*mmsdtng.*/:/' configs/default_hooks/sxmo_hook_start.sh

  sed -e 's/sxmo_daemons.sh start vvmd.*$/systemctl --user start vvmd.service/g' \
      -e 's/sxmo_daemons.sh stop vvmd/systemctl --user stop vvmd.service/g' \
      -i scripts/core/sxmo_vvmdconfig.sh
  sed -i 's/.*vvmd.*/:/' configs/default_hooks/sxmo_hook_start.sh

  patch -p1 < '../0001-Let-systemd-handle-service-supervision.patch'

  # A quick hack becaue $XDG_RUNTIME_DIR contains some files that aren't
  # accessable to the default user, which causes the dialer to fail
  sed -i '/^set -e$/d' scripts/modem/sxmo_modemdial.sh

  # Don't start a dbus session when starting sxmo
  sed -i 's@dbus-run-session@@g' scripts/core/sxmo_winit.sh
  sed -i 's@dbus-run-session@@g' scripts/core/sxmo_xinit.sh
}

package_sxmo-utils() {
  depends=(
    # Core cli dependencies
    'alsa-utils'
    'bluez'
    'bluez-utils'
    'busybox'
    'curl'
    'doas'
    'ffmpeg'
    'gawk'
    'geoclue'
    'inotify-tools'
    'gojq'
    'mmsd-tng'
    'mnc' # for suspend
    'modemmanager'
    'ncurses' # for the weather script
    'networkmanager'
    'pn'
    'v4l-utils'
    'vvmd'

    # Core GUI dependencies
    'sxmo-ui'
    'dunst'
    'libnotify'
    'light'
    'lisgd'
    'mediainfo'
    'mpv'
    'nerd-fonts-fira-mono' # system font
    'nerd-fonts-terminus'
    'bemenu'
    'bemenu-wayland')
  optdepends=('clickclack: haptic feedback'
              'codemadness-frontends: Youtube & Reddit scripts'
              'sfeed: Rss and atom feeds'
              'vim: The default editor'
              'yt-dlp: Play videos from the web')

  cd "$pkgname-$_pkgcommit"

  mkdir -p "$pkgdir/etc/modules-load.d/"
  printf %b "snd-aloop" > "$pkgdir/etc/modules-load.d/sxmo.conf"
  mkdir -p "$pkgdir/etc/modprobe.d/"
  printf %b "options snd slots=,snd-aloop" > "$pkgdir/etc/modprobe.d/sxmo.conf"

  make DESTDIR="$pkgdir" install

  # TODO: these should be installe to /usr if possible
  install -Dm644 "$srcdir/rootfs-etc-NetworkManager-conf.d-00-sxmo.conf" "$pkgdir/etc/NetworkManager/conf.d/00-sxmo.conf"
  install -Dm755 "$srcdir/rootfs-etc-polkit-1-rules.d-00-sxmo.rules" "$pkgdir/etc/polkit-1/rules.d/00-sxmo.rules"
  install -Dm755 "$srcdir/rootfs-etc-polkit-1-rules.d-50-org.freedesktop.NetworkManager.rules" "$pkgdir/etc/polkit-1/rules.d/50-org.freedesktop.NetworkManager.rules"

  # fix directory permissions
  # TODO: should be done upstream
  chmod 750 "$pkgdir/etc/polkit-1/rules.d"
  chmod 750 "$pkgdir/etc/doas.d"

  # HACK: doas is built without --with-doas-confdir so install the sxmo config directly
  echo "permit :wheel" > "$pkgdir/etc/doas.conf"
  cat "$pkgdir/etc/doas.d/sxmo.conf" >> "$pkgdir/etc/doas.conf"

  install -Dm644 "$srcdir/sxmo-setpermissions.service" "$pkgdir/usr/lib/systemd/system/sxmo-setpermissions.service"

  # Remove openrc components that aren't used on arch
  rm -r "$pkgdir/etc/init.d"

  # Remove session files, and only install them we install the ui package
  rm -r "$pkgdir/usr/share/wayland-sessions/"
  rm -r "$pkgdir/usr/share/xsessions/"
}

package_sxmo-ui-sway-meta() {
  pkgdesc="Packages required to use sway as a ui for sxmo"
  url="https://sr.ht/~mil/Sxmo/"
  arch=('any')
  provides=('sxmo-ui')
  install=
  depends=('foot'
           'grim'
           'mako'
           'seatd'
           'slurp'
           'sway'
           'swaybg'
           'swayidle'
           'wayout'
           'wl-clipboard'
           'wob'
           'wtype'
           'wvkbd'
           'xorg-xwayland')
  optdepends=('sxmo-sway: better touch event handling')

  install -Dm644 "$pkgbase-$_pkgcommit/configs/applications/swmo.desktop" \
    "$pkgdir/usr/share/wayland-sessions/swmo.desktop"
}

package_sxmo-ui-dwm-meta() {
  pkgdesc="Packages required to use dwm as a ui for sxmo"
  url="https://sr.ht/~mil/Sxmo/"
  arch=('any')
  provides=('sxmo-ui')
  install=
  depends=('autocutsel'
           'conky'
           'feh'
           'ttf-dejavu' # for conky
           'otf-latin-modern' # for conky
           'terminus-font' # dmenu & xcalc
           'svkbd'
           'sxiv'
           'sxmo-dmenu'
           'sxmo-dwm'
           'sxmo-st'
           'unclutter'
           'xcalib'
           'xclip'
           'xdotool'
           'xsel'
           'xorg-xdpyinfo'
           'xorg-xinput'
           'xorg-xprop'
           'xorg-xrandr'
           'xorg-xrdb'
           'xorg-xset'
           'xorg-xsetroot'
           'xorg-xwininfo'
           'xprintidle')

  install -Dm644 "$pkgbase-$_pkgcommit/configs/applications/sxmo.desktop" \
    "$pkgdir/usr/share/xsessions/sxmo.desktop"
}

sha512sums=('e598a4851f889e5d7d48f32c94eff048d864b061ef65e90f004be7657f6807a8b8bb2c75bbccef6a8c68f90da95a22f00f2f548711f95e3f065b6d959389b1f1'
            'abc5ccd74157f1188f2d43370d228a498017cb2a2e17aec131ab34329d95ed76d7323bf1d802754574c26a2463ffe4f426e0ac567e115f581bee8b0bcf9b64cd'
            '67a031f309a3232ac1e8abc3fedeaee912c035f9c81b4f709248895905a27ab5844ec92c65e55b79af3894450ba3883549d4004f11efebb47114d41f730e4a5f'
            '6e42f9acc015a21596cdf2d35eb5a80e8fbe97959aabf129861c8e1016bbebad5c706126730515494a0fcb33ad38fd6cd971048d2b5340557a803febf41967a4'
            '4c8f047c4608d89409a44db6370ca225d42e186323f42d0c564edb34f18c84a69a500a89cc2c31d5f0e5c292aa94ea20eaf8213e3e266b8f9e959c3d4ce9fb58'
            '532c3045f52f872704876bdf56afbc7470c5d17e46655dd4040d545b6d3eb34a8791909e554c560471f9494c7382107839bcff1e48be242d5eea0092482165cd')
