# Maintainer: Aren <aren@peacevolution.org>
# Contributor: Clayton Craft <clayton@craftyguy.net>

pkgname=superd
pkgver=0.7.1
pkgrel=1
pkgdesc='lightweight user service supervising daemon'
url='https://sr.ht/~craftyguy/superd'
arch=('x86_64' 'i686' 'armv7h' 'aarch64')
license=('GPL3')
makedepends=('go' 'scdoc')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~craftyguy/superd/archive/$pkgver.tar.gz")
sha256sums=('59de58f43bd237c12d0e73425df954eb14dd4e87e11d990e8191aa6921ee08d3')

build() {
	unset LDFLAGS # passed as go linker flags and invalid
	make -C "$pkgname-$pkgver" PREFIX="/usr" VERSION="$pkgver"
}

check() {
	cd "$pkgname-$pkgver"
	go test ./...
}

package() {
	make -C "$pkgname-$pkgver" PREFIX='/usr' DESTDIR="$pkgdir" install
}
