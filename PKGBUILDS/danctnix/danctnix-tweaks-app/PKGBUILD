# Maintainer: Danct12 <danct12@disroot.org>
pkgbase=danctnix-tweaks-app
pkgname=("$pkgbase" "$pkgbase-phosh" "$pkgbase-pinephone")
pkgver=0.8.1
pkgrel=1
pkgdesc="Tweaks tool for phone UIs"
url="https://github.com/dreemurrs-embedded/danctnix-tweaks"
license=('LGPL')
arch=('any')
makedepends=('libhandy' 'python' 'python-gobject' 'python-yaml' 'gtk3'
             'python-setuptools' 'meson' 'git')
_commit="e674c236781da03220878d8dc3b7da1f46ebb6f6" # tags/0.8.1
source=("git+https://github.com/dreemurrs-embedded/danctnix-tweaks.git#commit=$_commit")

pkgver() {
    cd danctnix-tweaks
    git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
    arch-meson danctnix-tweaks output
    meson compile -C output
}

package_danctnix-tweaks-app() {
    depends=('libhandy' 'python' 'python-gobject' 'python-yaml' 'gtk3')
    install=danctnix-tweaks-app.install

    DESTDIR="$pkgdir" meson install -C output
    mv "$pkgdir"/usr/share/danctnix-tweaks/phosh.yml "$srcdir"/phosh.yml
    mv "$pkgdir"/usr/share/danctnix-tweaks/pinephone.yml "$srcdir"/pinephone.yml

    # Not yet ready, we need the user to manually mkinitcpio after the config changes.
    rm "$pkgdir"/usr/share/danctnix-tweaks/unlocker.yml
}

package_danctnix-tweaks-app-phosh() {
     depends=('danctnix-tweaks-app' 'phosh')

     mkdir -p "$pkgdir"/usr/share/danctnix-tweaks
     mv "$srcdir"/phosh.yml "$pkgdir"/usr/share/danctnix-tweaks/phosh.yml
}

package_danctnix-tweaks-app-pinephone() {
     depends=('danctnix-tweaks-app' 'device-pine64-pinephone')

     mkdir -p "$pkgdir"/usr/share/danctnix-tweaks
     mv "$srcdir"/pinephone.yml "$pkgdir"/usr/share/danctnix-tweaks/pinephone.yml
}

md5sums=('SKIP')
