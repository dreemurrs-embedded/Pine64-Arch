# Maintainer: Bernhard Landauer <bernhard@manjaro.org>

pkgname=corrosion
pkgver=r165.gd93a9e0
pkgrel=1
pkgdesc='Tool for integrating Rust into an existing CMake project. Formerly known as cmake-cargo.'
arch=(x86_64 aarch64)
url="https://github.com/AndrewGaspar/$pkgname"
license=(MIT)
makedepends=(cmake git)
depends=(rust)
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -Scorrosion -Bbuild -DCMAKE_BUILD_TYPE=Release
  cmake --build build --config Release
}

package() {
  cmake --install build --config Release --prefix "${pkgdir}/usr"
}
