# Contributor: Andreas Radke <andyrtr@archlinux.org>

# DANCTNIX: - Add PinePhone keyboard config (8b4ca4168059f80330e792ffc432bf037fec90ff)

pkgname=xkeyboard-config
pkgver=2.35.1
pkgrel=3
pkgdesc="X keyboard configuration files"
arch=(any)
license=('custom')
url="https://gitlab.freedesktop.org/xkeyboard-config/xkeyboard-config"
makedepends=('xorg-xkbcomp' 'libxslt' 'python' 'meson')
provides=('xkbdata')
replaces=('xkbdata')
conflicts=('xkbdata')
source=(https://xorg.freedesktop.org/archive/individual/data/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        'Add-model-for-PinePhone-keyboard.patch'
        'Add-evdev-rule-for-ppkb-model.patch')
validpgpkeys=('FFB4CCD275AAA422F5F9808E0661D98FC933A145') # Sergey Udaltsov <sergey.udaltsov@gmail.com>
#validpgpkeys=('15CFA5C595041D2CCBEA155F1732AA424A0E86B4') # "Sergey Udaltsov (For GNOME-related tasks) <svu@gnome.org>"
sha256sums=('18ce50ff0c74ae6093062bce1aeab3d363913ea35162fe271f8a0ce399de85cc'
            'SKIP'
            '1c54ba3d1b4a0311ed69438aa3331558d1664b692f9e4f2e1231e85fb8587237'
            '2286decb583bdbe64d5db1184bd06df59eeaf72ea8018fb63ccb8af758a30448')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../Add-model-for-PinePhone-keyboard.patch
  patch -p1 < ../Add-evdev-rule-for-ppkb-model.patch
}

build() {
  arch-meson ${pkgname}-${pkgver} build \
	-D xkb-base="/usr/share/X11/xkb" \
	-D compat-rules=true \
	-D xorg-rules-symlinks=true

  # Print config
  meson configure build

  ninja -C build

 }
 
 package() { 

  DESTDIR="$pkgdir" ninja -C build install

  install -m755 -d "${pkgdir}/var/lib/xkb"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
