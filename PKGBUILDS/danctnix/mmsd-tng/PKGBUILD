# Maintainer: Aren <aren@peacevolution.org>
# Contributer: Caleb Fontenot <foley2431 at gmail dot com>

pkgname=mmsd-tng
pkgver=2.6.2
pkgrel=1
pkgdesc="Multimedia Messaging Service Daemon - The Next Generation"
url="https://gitlab.com/kop316/mmsd"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL-2.0-only')
provides=(mmsd)
conflicts=(mmsd)
depends=(
  'c-ares'
  'dbus'
  'libmm-glib'
  'libphonenumber'
  'libsoup3'
  'mobile-broadband-provider-info'
)
makedepends=(meson)
install=mmsd-tng.install
source=("$pkgname-$pkgver.tar.bz2::https://gitlab.com/kop316/mmsd/-/archive/$pkgver/mmsd-$pkgver.tar.bz2"
        "mmsd-tng.service")
sha512sums=('d3026d446a34aa1ae035cd56d109a7be6b02da1b5b323e2d775528123cf618bb9eda1cb395c20982487181eb9115b6fe6450e2548b74aa2bd85be04017dd4e3e'
            'df8ea75e7e1e66a65c96d7ba90fc97c09273af39df203000cb3b6f8795be9fc3991976ea4ab5e4a8b08de5238ce4eb32b869389e4e8a78d1d62343433308fb34')

build() {
  arch-meson "mmsd-$pkgver" build -Dbuild-mmsctl=true
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 "mmsd-tng.service" "$pkgdir/usr/lib/systemd/user/mmsd-tng.service"
}
