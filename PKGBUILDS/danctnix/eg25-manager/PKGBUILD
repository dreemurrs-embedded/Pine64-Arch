# Maintainer: Danct12 <danct12@disroot.org>
# Contributor : Philip MÃ¼ller <philm@manjaro.org>

pkgname=eg25-manager
pkgver=0.4.2
pkgrel=1
_commit=a91bc71e23e5a8d5e17b32bb5fa8f3691251dfeb
pkgdesc="Daemon for managing the Quectel EG25 modem"
arch=('aarch64')
url="https://gitlab.com/mobian1/devices/eg25-manager"
license=('GPL3')
depends=('libgudev' 'libgpiod' 'libusb' 'libmm-glib')
makedepends=('meson' 'ninja' 'git')
optdepends=('modemmanager: for Phosh'
            'ofono: for Lomiri or Plasma Mobile')
replaces=('danctnix-eg25-misc')
source=(git+$url.git#commit=${_commit})
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson ${pkgname} build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
