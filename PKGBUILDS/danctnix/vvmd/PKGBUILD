# Maintainer: Aren Moynihan <aren@peacevolution.org>
pkgname=vvmd
pkgver=0.7
pkgrel=1
pkgdesc="Visual Voicemail Daemon"
url="https://gitlab.com/kop316/vvmd"
arch=('x86_64' 'aarch64')
license=("GPL2")
install=vvmd.install
depends=("mobile-broadband-provider-info" "libmm-glib" "libphonenumber")
makedepends=("meson")
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/kop316/vvmd/-/archive/$pkgver/vvmd-$pkgver.tar.gz"
        "vvmd.service")
sha256sums=('b2268a2cca5f2f6b4fa57178e05a5aef898911b770dc0eb7266ea96eeb97a62b'
            'b4bfd3b3758427975eee8ca5b4a39fb43ce7ea36b56d7c82a23fdacdb4161c50')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 "vvmd.service" "$pkgdir/usr/lib/systemd/user/vvmd.service"
}
