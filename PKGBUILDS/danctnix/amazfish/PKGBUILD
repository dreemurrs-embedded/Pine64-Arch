# Maintainer: Bernhard Landauer <bernhard@manjaro.org>

pkgname=amazfish
_repo=harbour-$pkgname
pkgver=2.0.3
pkgrel=3
pkgdesc='Companion application for the Huami Amazfit Bip, Cor, MiBand2/3 and GTS'
arch=('x86_64' 'aarch64')
license=(GPL3)
url="https://github.com/piggz/$_repo"
makedepends=('git')
depends=('kcontacts' 'dbus' 'kdb' 'kirigami2' 'mlite' 'qtmpris' 'qt5-connectivity' 'qt5-location' 'qt5-quickcontrols2' 'nemo-qml-plugin-dbus')
_commit="af26b03028c73ef5db8bd11c974a1e3b57e2d957" # v2.0.3
_commit_libwatchfish="ec302cd25d4605e56148a750f2f75fb5c641cfee"
source=("git+$url.git#commit=$_commit"
    "git+https://github.com/piggz/libwatchfish.git#commit=$_commit_libwatchfish")
md5sums=('SKIP'
         'SKIP')

prepare() {
  cd $_repo
  git submodule init
  git config submodule.libwatchfish.git.url $srcdir/libwatchfish
  git submodule update
}

build() {
  cd $_repo
  qmake-qt5 FLAVOR=kirigami PREFIX=/usr
  make
}
 
package() {
  cd $_repo
  make INSTALL_ROOT="$pkgdir" install
}
