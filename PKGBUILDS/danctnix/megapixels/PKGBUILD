# Maintainer: Danct12 <danct12@disroot.org>
pkgname=megapixels
pkgver=1.4.3
pkgrel=2
pkgdesc="A GTK4 camera application that knows how to deal with the media request API"
url="https://gitlab.com/postmarketOS/megapixels"
license=('GPL')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('glib2' 'gtk4' 'imagemagick' 'libraw' 'zbar')
makedepends=('meson' 'ninja')
replaces=('pinhole' 'gnome-camera')
source=("https://gitlab.com/postmarketOS/megapixels/-/archive/${pkgver}/megapixels-${pkgver}.tar.gz"
        '90-megapixels.rules')
sha256sums=('8976fa727fa5650499fe1ae923a93d56fedd62e02d92def24d4bf4b0d25a902f'
            'c9ff69c5963f6231ac6a7de5adc3e0327f47b501de9b084b49546bfb00e7fc58')

build() {
    arch-meson $pkgname-$pkgver build
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install

    install -Dm644 "${srcdir}/90-megapixels.rules" -t "${pkgdir}/usr/lib/udev/rules.d/"
}
