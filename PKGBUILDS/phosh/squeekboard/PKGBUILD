# Maintainer: Danct12 <danct12@disroot.org>
pkgname=squeekboard
pkgver=1.22.0
pkgrel=1
pkgdesc="An on-screen-keyboard input method for Wayland"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/squeekboard"
license=('GPL')
depends=('feedbackd' 'gtk3' 'gnome-desktop' 'libbsd' 'wayland-protocols')
makedepends=('rust' 'cargo' 'meson' 'ninja')
source=(https://gitlab.gnome.org/World/Phosh/squeekboard/-/archive/v$pkgver/squeekboard-v$pkgver.tar.gz)

build() {
    arch-meson ${pkgname}-v${pkgver} output -Dtests=false
    ninja -C output
}

check() {
    meson test -C output --print-errorlogs
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}

md5sums=('d74a2b66b3997baf5ef453dd4fd07058')
