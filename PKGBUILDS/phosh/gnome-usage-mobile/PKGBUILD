# Maintainer: Philip Müller <philm at manjaro dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-usage-mobile
_pkgname=gnome-usage
pkgver=3.38.1
pkgrel=1
pkgdesc="GNOME application to view information about use of system resources"
arch=(aarch64)
url="https://wiki.gnome.org/Apps/Usage"
license=(GPL3)
depends=(gtk3 libgtop libdazzle tracker libhandy gtherm)
makedepends=(git meson vala accountsservice gamemode)
optdepends=('accountsservice: show user tags for processes'
            'gamemode: show processes requesting game mode')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
_commit=4745bb5e8f2a44a35cb410020dcf6e54e73bd6e1  # tags/3.38.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-usage.git#commit=$_commit"
        '0001-Add-libgtherm-dependency.patch'
        '0002-Add-initial-thermal-view.patch'
        'Revert-Port-to-Tracker-3.patch')
sha256sums=('SKIP'
            'b7845a5717334e043171401188165bd5050f672fac3c73b9624b750217a6d377'
            '0caa915c6d9044d57af44e9723efcfb6be1052e8b360b2c3f35dc2ef8499f38c'
            'caee17d5e8d5adc04bfe6fcdc08e39ec8174b2e97982dbd48a75c20656b5984e')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
