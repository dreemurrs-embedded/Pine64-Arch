# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts-mobile
_pkgname=gnome-contacts
pkgver=45.0
pkgrel=1
pkgdesc="Contacts Manager for GNOME - Forked for Purism patches"
url="https://wiki.gnome.org/Apps/Contacts"
arch=(x86_64 armv7h aarch64)
license=(GPL2)
depends=(
  cairo
  dconf
  evolution-data-server
  folks
  gdk-pixbuf2
  glib2
  gnome-online-accounts
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libgoa
  libportal
  libportal-gtk4
  pango
  qrencode
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
)
provides=(gnome-contacts)
conflicts=(gnome-contacts)
_commit=f100e20eb51fac292b763aca06f7d963a2d1b9f3  # tags/45.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-contacts.git#commit=$_commit"
        '0001-ContactSheet-Add-make-call-and-send-sms-button.patch')
b2sums=('SKIP'
        'fa242d8e587486496502cafbb565423752c7f3d49c15bc364a26f64bb81ddbe070f900fa68c8c24d863a894123416ecef7b9b9902d2acbe077523a036d3ccf5c')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
