# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts-mobile
_pkgname=gnome-contacts
pkgver=40.0
pkgrel=1
pkgdesc="Contacts Manager for GNOME - Forked for Purism patches"
url="https://wiki.gnome.org/Apps/Contacts"
arch=(x86_64 armv7h aarch64)
license=(GPL2)
depends=(gtk3 folks gnome-desktop dconf gnome-online-accounts libgee libhandy)
makedepends=(vala gobject-introspection git meson)
provides=(gnome-contacts)
conflicts=(gnome-contacts)
_commit=8c27ab8cb5e7fcdfe40a27e9744bd1299360a7c4  # tags/40.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-contacts.git#commit=$_commit"
  0001-ContactSheet-Add-make-call-and-send-sms-button.patch
  0002-Contact-Sheet-use-normalized-phone-number-for-tel-and-sms-.patch
  0003-Sheet-use-user-available-symbolic-for-send-sms-btn.patch
  0004-ContactSheet-always-show-im-details.patch)
sha256sums=('SKIP'
            '7067e74d67222cefb3a91d998f30e88b7c1248fce585cfdd8573e48f6522e5ee'
            '85e9fccfbcdb6001b37612e8da7278f39a0667f64cd806f60b9c883af462163e'
            'a41b6d355199456ee00b6df2b421bc09742237b0e8f25de91d6cbb17ade53e8a'
            'f2f5cab652db6e76ae839669c1f16db9fb2c286ad609e2fbb9926be6c4034d9b')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build -Ddocs=true -Dcheese=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
