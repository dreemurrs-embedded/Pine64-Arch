# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts-mobile
_pkgname=gnome-contacts
pkgver=44.0
pkgrel=1
pkgdesc="Contacts Manager for GNOME - Forked for Purism patches"
url="https://wiki.gnome.org/Apps/Contacts"
arch=(x86_64 armv7h aarch64)
license=(GPL2)
depends=(gtk4 folks gnome-online-accounts libgee libadwaita libportal-gtk4
         evolution-data-server qrencode)
makedepends=(vala gobject-introspection git meson appstream-glib)
provides=(gnome-contacts)
conflicts=(gnome-contacts)
_commit=7de4264212aac05f8acec29ba54adac90ae8ed39  # tags/44.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-contacts.git#commit=$_commit"
        '0001-ContactSheet-Add-make-call-and-send-sms-button.patch')
sha256sums=('SKIP'
            '30b1123c00025d67e73cdded705c740860dde5e55e4e88f580f3b57a8f37069c')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
