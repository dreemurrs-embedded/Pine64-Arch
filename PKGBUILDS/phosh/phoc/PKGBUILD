# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc
pkgver=0.33.0
pkgrel=1
pkgdesc="wlroots based Wayland compositor for mobile phones"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phoc"
license=('GPL')
depends=(
  'gobject-introspection'
  'gnome-desktop'
  'libglvnd'
  'libinput'
  'libxcb'
  'mutter'
  'seatd'
  'xcb-util-errors'
  'xcb-util-renderutil'
  'xcb-util-wm'
  'wayland-protocols'
)
makedepends=(
  'git'
  'glslang'
  'meson'
  'ninja'
  'vulkan-headers'
)
checkdepends=('xorg-server-xvfb')
_commit="2701b187218f70858c596fded81fbe9ba63a86c1"  # tags/0.33.0
source=("git+https://gitlab.gnome.org/World/Phosh/phoc.git#commit=${_commit}")

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  local meson_options=(
    --wrap-mode default
    -D embed-wlroots=enabled
    --default-library=static
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  WLR_RENDERER=pixman xvfb-run ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja install -C build

  # Install scale-to-fit helper
  install -Dm755 "$pkgname"/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit

  # Remove unnecessary files
  rm -r "$pkgdir"/usr/lib
  rm -r "$pkgdir"/usr/include
}

sha256sums=('SKIP')
