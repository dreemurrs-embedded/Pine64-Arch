# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc
pkgver=0.23.0
pkgrel=1
pkgdesc="A compositor for phones based on wlroots"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phoc"
license=('GPL')
depends=('gobject-introspection' 'gnome-desktop' 'libinput' 'seatd' 'mutter'
         'xcb-util-errors' 'xcb-util-renderutil' 'xcb-util-wm' 'wayland-protocols'
         'libvulkan.so')
makedepends=('git' 'meson' 'ninja' 'glslang' 'vulkan-headers')
checkdepends=('xorg-server-xvfb')
_commit="d14ed975c6e2f2410e918cb0529883d84a047c93"
_commit_wlroots="06f3b6e91e17b8bdc76f3f40216c62e8ba5fe6d6"
source=("git+https://gitlab.gnome.org/World/Phosh/phoc.git#commit=${_commit}"
        "wlroots-phoc::git+https://source.puri.sm/Librem5/wlroots.git#commit=${_commit_wlroots}")

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init subprojects/wlroots
  git submodule set-url subprojects/wlroots "$srcdir/wlroots-phoc"
  git submodule--helper update
}

build() {
  local meson_options=(
    -D embed-wlroots=enabled
    --default-library=static
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s -noreset meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # Install scale-to-fit helper
  install -Dm755 "$pkgname"/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit

  # Remove unnecessary files
  rm -r "$pkgdir"/usr/lib
  rm -r "$pkgdir"/usr/include
}

sha256sums=('SKIP'
            'SKIP')
