# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phoc
pkgver=0.7.0
pkgrel=2
pkgdesc="A compositor for phones based on wlroots"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://source.puri.sm/Librem5/phoc"
license=('GPL')
depends=('gobject-introspection' 'gnome-desktop' 'libinput' 'mutter'
         'xcb-util-errors' 'xcb-util-wm')
makedepends=('meson' 'ninja')

# We don't want to maintain the wlroots package, so we static compile it
_wlroots_ver="0.12.0"
source=(https://source.puri.sm/Librem5/phoc/-/archive/v$pkgver/phoc-v$pkgver.tar.gz
        https://github.com/swaywm/wlroots/releases/download/${_wlroots_ver}/wlroots-${_wlroots_ver}.tar.gz
        Revert-layer-shell-error-on-0-dimension-without-anchors.diff)

prepare() {
	rm -r ${pkgname}-v${pkgver}/subprojects/wlroots
	mv wlroots-${_wlroots_ver} ${pkgname}-v${pkgver}/subprojects/wlroots

	cd ${pkgname}-v${pkgver}/subprojects/wlroots
	patch -p1 -N < ${srcdir}/Revert-layer-shell-error-on-0-dimension-without-anchors.diff
}

build() {
	arch-meson ${pkgname}-v${pkgver} output -Dtests=false \
		-Dwlroots:logind-provider=systemd \
		-Dwlroots:libseat=disabled
	ninja -C output
}

package() {
	DESTDIR="$pkgdir" ninja -C output install

	# Install scale-to-fit helper
	install -Dm755 ${pkgname}-v${pkgver}/helpers/scale-to-fit "$pkgdir"/usr/bin/scale-to-fit

	# Remove unnecessary files
	rm -r "$pkgdir"/usr/lib
	rm -r "$pkgdir"/usr/include
}

md5sums=('9339ce69a2d30066569a0ba4a208c5d8'
         'bc9dbfef37385dbe0f4fe129d2329be5'
         '1ddc7fac33b1a20da0a9fd688453d292')
