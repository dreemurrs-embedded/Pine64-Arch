# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=evince
pkgname=$_pkgname-mobile
pkgver=3.34.1
pkgrel=0
pkgdesc='Purism mobile document viewer (PDF, Postscript, djvu, tiff, dvi, XPS, SyncTex support with gedit, comics books (cbr,cbz,cb7 and cbt))'
url='https://source.puri.sm/Librem5/evince'
arch=('aarch64')
license=('GPL')
_commit=2fa24ebae9deb0fabb5dd50dff523ab7a79d2237
source=("git+https://source.puri.sm/Librem5/evince.git#commit=$_commit")
sha256sums=('SKIP')
provides=(libev{document,view}3.so)
groups=(gnome)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre t1lib dconf libsecret libsynctex gsettings-desktop-schemas gnome-desktop libarchive gst-plugins-base-libs gspell)
makedepends=(libnautilus-extension texlive-bin gobject-introspection docbook-xsl python gtk-doc git meson appstream-glib yelp-tools)

optdepends=(
  'texlive-bin: DVI support'
  'gvfs: bookmark support and session saving'
)

build() {
  arch-meson $_pkgname build -D ps=enabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
