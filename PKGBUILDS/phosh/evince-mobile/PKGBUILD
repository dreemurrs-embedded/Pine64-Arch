# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=evince
pkgname=$_pkgname-mobile
pkgver=45.0
pkgrel=1
pkgdesc="Document viewer (PDF, PostScript, XPS, djvu, dvi, tiff, cbr, cbz, cb7, cbt)"
url="https://wiki.gnome.org/Apps/Evince"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL')
depends=(
  dconf
  djvulibre
  gnome-desktop
  gsettings-desktop-schemas
  gsfonts
  gspell
  gst-plugins-base-libs
  gtk3
  gvfs
  libarchive
  libgxps
  libhandy
  libsecret
  libspectre
  libsynctex
  libxml2
  poppler-glib
)
makedepends=(
  appstream-glib
  gi-docgen
  git
  gobject-introspection
  meson
  texlive-bin
  yelp-tools
)
optdepends=('texlive-bin: DVI support')
provides=(libev{document,view}3.so $_pkgname)
conflicts=($_pkgname)
_commit=80b393cac6468561664810d5e4a0f1897507062b  # tags/45.0^0
source=("git+https://gitlab.gnome.org/GNOME/evince.git#commit=$_commit"
        '0001-toolbar-Allow-to-toggle-the-sidebar-button.patch'
        '0002-toolbar-Allow-hiding-zoom-annotation-and-page-widgets.patch'
        '0003-properties-view-Reduce-the-label-width-chars.patch'
        '0004-window-Port-it-to-phones.patch')
sha256sums=('SKIP'
            '22a5c037d60cce4869193a294f90c071dab2a47ff97f9632eb22ba0f8377b4a0'
            '0153d21c3da3a65c28c246bc31a44b307c1af4e6e56f897b8e8ec15b92974df3'
            'b184beef8d11f3d1196869744865b94343b75440f326582e51cb7fb3f4beb176'
            'f125e441c0d5cc80599859347903f6b3e1d82cab25b30df508ed0bb6892c8a45')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  local meson_options=(
    -D ps=enabled
  )

  arch-meson $_pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  # We don't need to provide evince-lib-docs.
  rm -r "$pkgdir"/usr/share/doc/lib*
}
