# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=evince
pkgname=$_pkgname-mobile
pkgver=41.3
pkgrel=1
pkgdesc='Purism mobile document viewer (PDF, Postscript, djvu, tiff, dvi, XPS, SyncTex support with gedit, comics books (cbr,cbz,cb7 and cbt))'
url='https://gitlab.gnome.org/GNOME/evince'
arch=('aarch64')
license=('GPL')
_commit=08780f527ac0306f527c48ea6cc77a7b2c6c5fa8  # tags/41.3^0
source=("git+https://gitlab.gnome.org/GNOME/evince.git#commit=$_commit"
        'data-List-compatible-form-factors.patch'
        'properties-view-Reduce-the-label-width-chars.patch'
        'Disable-fullscreen-actions-in-phosh.patch'
        'toolbar-Allow-to-toggle-the-sidebar-button.patch'
        'toolbar-Allow-hiding-zoom-annotation-and-page-widgets.patch'
        'window-Port-it-to-phones.patch')
sha256sums=('SKIP'
            '84f300a09dd5435647b1f7837db8f11d149d7add5d62779ed26a03eb35f1444d'
            '194086757f539afc7cd5d24f4bc3e1b16fa321d5cb6c9e27fe0470eb62508098'
            'fa8554c29ef59ad926bc33adead0c4b327cc14c5b444f4fbaa897c76fb67f1c3'
            '3ce259a56cc4f35badbdc142b83c72a4fe1422aeb483979ec343ab8a68a76302'
            '6dbd8d185cd896e1ab772f2aee6147113333109ea8f0e8446335f4da6a9f2bb9'
            '00413c8f03b75a309c02acb2ab9ea5b52d4f4096994e0bec691fd998c2ed8ef5')
provides=(libev{document,view}3.so $_pkgname)
conflicts=($_pkgname)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre t1lib dconf libsecret libsynctex
         gsettings-desktop-schemas gnome-desktop libarchive gst-plugins-base-libs gspell libhandy)
makedepends=(libnautilus-extension texlive-bin gobject-introspection docbook-xsl python gtk-doc
             git meson appstream-glib yelp-tools)

optdepends=(
  'texlive-bin: DVI support'
  'gvfs: bookmark support and session saving'
)

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build -D ps=enabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
