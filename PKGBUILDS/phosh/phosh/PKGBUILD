# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.20.0
pkgrel=1
pkgdesc="The Librem5 Shell"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL')
depends=('gtk3' 'libhandy>=1.1.90' 'gnome-desktop' 'gnome-session' 'gnome-shell' 'upower' 'libpulse'
         'gcr' 'feedbackd' 'libnm' 'phoc' 'callaudiod')
makedepends=('meson' 'ninja' 'git')
_commit="5fe9aea775b9198eed25fb3b3bcd9827ae40a785" # tags/v0.20.0
source=("phosh::git+https://gitlab.gnome.org/World/Phosh/phosh.git#commit=${_commit}"
        'pam_phosh'
        'sm.puri.OSK0.desktop'
        'phosh.service')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd phosh
    git submodule update --init

    # At the time of 0.17.0 release, GNOME 42 hasn't arrived on Arch Linux yet.
    git revert -n bc26bf0827abe4d841dcf252b9f5195de4b4c691
}

build() {
    arch-meson phosh output -Dsystemd=true
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    install -Dm644 "$srcdir"/sm.puri.OSK0.desktop \
        "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('SKIP'
         '6d5a0d561f8362bf91f85c236a88395d'
         '30c7febb5cbbac40984fb50d66a88639'
         '60c659ae0e643bb63f7fbc484a8d9666')
