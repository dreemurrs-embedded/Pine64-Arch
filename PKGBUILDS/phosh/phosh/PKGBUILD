# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.34.1
pkgrel=1
pkgdesc="A Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL')
depends=(
    'callaudiod'
    'evince'
    'evolution-data-server'
    'feedbackd'
    'gcr'
    'gtk3'
    'libadwaita'
    'libhandy'
    'libnm'
    'libpulse'
    'gnome-desktop'
    'gnome-session'
    'gnome-shell'
    'phoc'
    'squeekboard'
    'upower'
)
makedepends=(
    'git'
    'meson'
    'ninja'
    'wayland-protocols'
)
checkdepends=('xorg-server-xvfb')
install="$pkgname.install"
_commit="a70e81cf22d124570bc209ee5a4bc8caa4591587" # tags/v0.34.1
source=("phosh::git+https://gitlab.gnome.org/World/Phosh/phosh.git#commit=${_commit}"
        'pam_phosh'
        'phosh.service')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd phosh
    git submodule update --init
}

build() {
    arch-meson phosh output --libexecdir=/usr/lib/$pkgname -Dphoc_tests=disabled -Dsystemd=true
    ninja -C output
}

check() {
    xvfb-run ninja test -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    mkdir -p "$pkgdir"/usr/share/applications
    ln -s sm.puri.Squeekboard.desktop "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('SKIP'
         '6d5a0d561f8362bf91f85c236a88395d'
         '8d95d47dba9aede4db4e643d59749093')
