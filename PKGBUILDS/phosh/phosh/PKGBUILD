# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.31.1
pkgrel=1
pkgdesc="A Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL')
depends=('gtk3' 'libhandy' 'phoc' 'gnome-desktop' 'gnome-session' 'gnome-shell' 'libadwaita' 'upower' 'libpulse'
         'gcr' 'feedbackd' 'libnm' 'phoc' 'callaudiod' 'evolution-data-server' 'evince' 'squeekboard')
makedepends=('meson' 'ninja' 'git' 'wayland-protocols')
checkdepends=('xorg-server-xvfb')
install="$pkgname.install"
_commit="691d08e675ddbd9eea4ebd400f780374ea3f4094" # tags/v0.31.1
source=("phosh::git+https://gitlab.gnome.org/World/Phosh/phosh.git#commit=${_commit}"
        'osk-button-remove-margins.patch'
        'pam_phosh'
        'phosh.service')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd phosh
    git submodule update --init

    patch -p1 < ../osk-button-remove-margins.patch
}

build() {
    arch-meson phosh output --libexecdir=/usr/lib/$pkgname -Dphoc_tests=disabled -Dsystemd=true
    ninja -C output
}

check() {
    xvfb-run ninja test -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    mkdir -p "$pkgdir"/usr/share/applications
    ln -s sm.puri.Squeekboard.desktop "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('SKIP'
         'cf968e048f074d9d3b4854751363bad7'
         '6d5a0d561f8362bf91f85c236a88395d'
         '8d95d47dba9aede4db4e643d59749093')
