# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.21.0
pkgrel=1
pkgdesc="The Librem5 Shell"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL')
depends=('gtk3' 'libhandy>=1.1.90' 'gnome-desktop' 'gnome-session' 'gnome-shell' 'upower' 'libpulse'
         'gcr' 'feedbackd' 'libnm' 'phoc' 'callaudiod')
makedepends=('meson' 'ninja' 'git')
_commit="7993b28d1cf58356fc9f0246bdf8e66fe91071ed"  # tags/v0.21.0
source=("phosh::git+https://gitlab.gnome.org/World/Phosh/phosh.git#commit=${_commit}"
        'fix-animations-crash.patch'
        'pam_phosh'
        'sm.puri.OSK0.desktop'
        'phosh.service')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd phosh
    git submodule update --init

    patch -p1 < ../fix-animations-crash.patch
}

build() {
    arch-meson phosh output --libexecdir=/usr/lib/$pkgname -Dsystemd=true
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    install -Dm644 "$srcdir"/sm.puri.OSK0.desktop \
        "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('SKIP'
         '7053d4ff44bd003b1d72dcf1a42581fb'
         '6d5a0d561f8362bf91f85c236a88395d'
         '30c7febb5cbbac40984fb50d66a88639'
         '60c659ae0e643bb63f7fbc484a8d9666')
