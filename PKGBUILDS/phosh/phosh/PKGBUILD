# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.21.0
pkgrel=1
pkgdesc="A Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL')
depends=('gtk3' 'libhandy>=1.1.90' 'gnome-desktop' 'gnome-session' 'gnome-shell' 'upower' 'libpulse'
         'gcr' 'feedbackd' 'libnm' 'phoc' 'callaudiod' 'evolution-data-server')
makedepends=('meson' 'ninja' 'git')
_commit="4122630266abfd6623e169330da6e7d6bc01be7f"  # tags/v0.21.0
source=("phosh::git+https://gitlab.gnome.org/World/Phosh/phosh.git#commit=${_commit}"
        'pam_phosh'
        'sm.puri.OSK0.desktop'
        'phosh.service')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd phosh
    git submodule update --init
}

build() {
    arch-meson phosh output --libexecdir=/usr/lib/$pkgname -Dsystemd=true
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    install -Dm644 "$srcdir"/sm.puri.OSK0.desktop \
        "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('SKIP'
         '6d5a0d561f8362bf91f85c236a88395d'
         '30c7febb5cbbac40984fb50d66a88639'
         '60c659ae0e643bb63f7fbc484a8d9666')
