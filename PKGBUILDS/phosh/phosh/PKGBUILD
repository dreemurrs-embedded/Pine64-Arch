# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.27.0
pkgrel=1
pkgdesc="A Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL')
depends=('gtk3' 'libhandy' 'phoc' 'gnome-desktop' 'gnome-session' 'gnome-shell' 'libadwaita' 'upower' 'libpulse'
         'gcr' 'feedbackd' 'libnm' 'phoc' 'callaudiod' 'evolution-data-server' 'evince')
makedepends=('meson' 'ninja' 'git' 'wayland-protocols')
_commit="8ed96ed0a6de978b778b2d845c6e67b3eeaa13c3"  # tags/v0.27.0
_libcall_ui_commit="6798b38d4d66d069751151b3e9a202c6de8d7f3c" # tags/v0.1.0
source=("phosh::git+https://gitlab.gnome.org/World/Phosh/phosh.git#commit=${_commit}"
        'pam_phosh'
        'sm.puri.OSK0.desktop'
        'phosh.service')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd phosh
    git submodule update --init

    # change libcall-ui commit
    cd subprojects/libcall-ui
    git checkout 619dd91561ad470db3d0e0e263ebc35d787afd2e
}

build() {
    arch-meson phosh output --libexecdir=/usr/lib/$pkgname -Dsystemd=true
    ninja -C output
}

package() {
    DESTDIR="$pkgdir" ninja -C output install

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    install -Dm644 "$srcdir"/sm.puri.OSK0.desktop \
        "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}

md5sums=('SKIP'
         '6d5a0d561f8362bf91f85c236a88395d'
         '30c7febb5cbbac40984fb50d66a88639'
         '8d95d47dba9aede4db4e643d59749093')
