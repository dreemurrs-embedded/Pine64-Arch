# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.38.0
pkgrel=1
pkgdesc="A Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL-3.0-or-later')
depends=(
    'callaudiod'
    'evince'
    'evolution-data-server'
    'feedbackd'
    'gcr'
    'gtk3'
    'libadwaita'
    'libcap'
    'libhandy'
    'libnm'
    'libpulse'
    'gnome-desktop'
    'gnome-session'
    'gnome-shell'
    'phoc'
    'squeekboard'
    'upower'
)
makedepends=(
    'gi-docgen'
    'meson'
    'python-docutils'
    'python-packaging'
    'wayland-protocols'
)
checkdepends=('xorg-server-xvfb')
install="$pkgname.install"
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
        https://gitlab.gnome.org/World/Phosh/phosh/-/commit/eca607733788dd141692f467b4b7c96520c513d3.patch
        'pam_phosh'
        'phosh.service')
sha256sums=('c27ae4145ceed0e0536533b9c2bf5f756297bbdccb2f6e8c7beb80d2c9be9806'
            'SKIP'
            '08760fd005a01bc9e97e8b893eb749dffb992828043ddc22ff826b6556151631'
            '43b94d0d9f4d083f028c77d18cb0d0f8037d160c41f333878c7cae3df0163c3d'
            '48dfe40c42218c92eec59969c06f203dc8a3928b21ca9ee2b92b0987de874a66')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i "$srcdir"/eca607733788dd141692f467b4b7c96520c513d3.patch
}

build() {
    local meson_options=(
        --libexecdir=/usr/lib/$pkgname
        -D gtk_doc=true
        -D man=true
        -D phoc_tests=disabled
        -D systemd=true
    )
    arch-meson $pkgname-$pkgver output "${meson_options[@]}"
    meson compile -C output
}

check() {
    xvfb-run meson test -C output --print-errorlogs
}

package() {
    meson install -C output --destdir "$pkgdir"

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    mkdir -p "$pkgdir"/usr/share/applications
    ln -s sm.puri.Squeekboard.desktop "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}
