# Maintainer: Danct12 <danct12@disroot.org>
pkgname=phosh
pkgver=0.43.0
pkgrel=1
pkgdesc="Wayland shell for GNOME on mobile devices."
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/World/Phosh/phosh"
license=('GPL-3.0-or-later')
depends=(
    'callaudiod'
    'evince'
    'evolution-data-server'
    'feedbackd'
    'gcr'
    'gtk3'
    'libadwaita'
    'libcap'
    'libgmobile'
    'libhandy'
    'libnm'
    'libpulse'
    'gnome-bluetooth-3.0'
    'gnome-desktop'
    'gnome-session'
    'gnome-shell'
    'phoc'
    'squeekboard'
    'upower'
)
makedepends=(
    'gi-docgen'
    'glib2-devel'
    'meson'
    'python-docutils'
    'python-packaging'
    'wayland-protocols'
)
checkdepends=('xorg-server-xvfb')
install="$pkgname.install"
source=(https://sources.phosh.mobi/releases/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
        'pam_phosh'
        'phosh.service')
b2sums=('ea9763bbf9f73d3a5ee9a924fe655d156ddca7104ee5abdb19a277e74a1a3088b7d70bbaab815db1b17532901fb248ad42180cad6bedaeece5edcb97b2079421'
        'SKIP'
        'd89e16605e2e8e29834f9537bd09ef3f76f1cf6420cdd99346a760cab8c7217f86898be66c24b206b464239fc8b5277150abd19bae81af72187a435bd8c2f869'
        '5a769e4bda9c2c23cc386ac657dfb4267f130ee22644c3bf247f7373ee53afcf30d3086906e020d005e0b860742356f004cc5543bd1ed62fb82fb71d7e9b430e')
validpgpkeys=('0DB3932762F78E592F6522AFBB5A2C77584122D3')

prepare() {
    cd $pkgname-$pkgver
}

build() {
    local meson_options=(
        --libexecdir=/usr/lib/$pkgname
        -D gtk_doc=true
        -D man=true
        -D phoc_tests=disabled
    )
    arch-meson $pkgname-$pkgver output "${meson_options[@]}"
    meson compile -C output
}

check() {
    xvfb-run meson test -C output --print-errorlogs
}

package() {
    meson install -C output --destdir "$pkgdir"

    install -Dm644 "$srcdir"/phosh.service \
        "$pkgdir"/usr/lib/systemd/system/phosh.service
    install -Dm644 "$srcdir"/pam_phosh \
        "$pkgdir"/etc/pam.d/phosh
    mkdir -p "$pkgdir"/usr/share/applications
    ln -s sm.puri.Squeekboard.desktop "$pkgdir"/usr/share/applications/sm.puri.OSK0.desktop
}
