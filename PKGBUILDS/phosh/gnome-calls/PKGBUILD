# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Carl Smedstad <carsme@archlinux.org>
# Contributor: Yassine Oudjana <y.oudjana@protonmail.com>
# Contributor: Philip Goto <philip.goto@gmail.com>
# Contributor: GI Jack <GI_Jack@hackermail.com>

pkgname=gnome-calls
pkgver=47.0
pkgrel=1
pkgdesc="Phone dialer and call handler"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://gitlab.gnome.org/GNOME/calls"
license=(GPL-3.0-or-later)
depends=(
  callaudiod
  dconf
  evolution-data-server
  feedbackd
  folks
  gcc-libs
  glib2
  glibc
  gom
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgee
  libmm-glib
  libpeas-2
  libsecret
  sofia-sip
)
makedepends=(
  appstream
  git
  glib2-devel
  meson
  python-docutils
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/calls.git?signed#tag=v${pkgver/[a-z]/_&}"
        "git+https://gitlab.gnome.org/World/Phosh/libcall-ui.git")
b2sums=('c5414f48e994c03fdfb48a0a61185d10dfc85cf6ed65a834c83a48bdefadb3f320c8dda3790024a2def7a99dac6152b5d672291af375cd56714143d8151f0d1c'
        'SKIP')
validpgpkeys=(
  B9386554B7DD266BCB8E29A990F0C9B18A6B4A19 # Evangelos Ribeiro Tzaras <devrtz@fortysixandtwo.eu>
)

prepare() {
  cd calls

  git submodule init
  git submodule set-url subprojects/libcall-ui "${srcdir}/libcall-ui"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D tests=false
  )

  arch-meson calls build "${meson_options[@]}"
}

package() {
  meson install -C build --destdir "${pkgdir}"
}
