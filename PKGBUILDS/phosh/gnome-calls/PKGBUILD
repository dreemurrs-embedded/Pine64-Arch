# Maintainer: Danct12 <danct12@disroot.org>
pkgname=gnome-calls
pkgver=43.0
pkgrel=1
pkgdesc="Make and receive PSTN phone calls"
url="https://gitlab.gnome.org/GNOME/calls"
license=('GPL')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('cmocka' 'evolution-data-server' 'feedbackd' 'folks' 'gobject-introspection'
         'gom' 'gtk3' 'libhandy' 'libpeas' 'modemmanager' 'callaudiod' 'libsecret' 'sofia-sip'
         'wayland-protocols')
makedepends=('appstream' 'python-docutils' 'meson' 'ninja' 'vala' 'git')
provides=("calls")
replaces=('calls')
_commit="fcee1a3b6a03383974d67ca67701eba246a646ae" # tags/43.0
source=("git+https://gitlab.gnome.org/GNOME/calls.git#commit=$_commit"
        "git+https://gitlab.gnome.org/World/Phosh/libcall-ui.git")

prepare() {
  cd calls
  git submodule init
  git submodule set-url subprojects/libcall-ui "$srcdir/libcall-ui"
  git submodule update

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson calls output
  ninja -C output
}

package() {
  DESTDIR="$pkgdir" ninja -C output install
}

md5sums=('SKIP'
         'SKIP')
