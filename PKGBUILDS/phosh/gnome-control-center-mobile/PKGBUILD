# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=gnome-control-center
pkgname=gnome-control-center-mobile
pkgver=43.6
pkgrel=1
pkgdesc="GNOME's main interface to configure various aspects of the desktop - Purism fork"
url="https://gitlab.gnome.org/GNOME/gnome-control-center"
license=(GPL3)
arch=(x86_64 armv7h aarch64)
depends=(
  accountsservice
  bolt
  colord-gtk4
  cups-pk-helper
  gcr
  gnome-bluetooth-3.0
  gnome-color-manager
  gnome-desktop-4
  gnome-online-accounts
  gnome-settings-daemon
  gsettings-desktop-schemas
  gsound
  gtk4
  libadwaita
  libgnomekbd
  libgtop
  libgudev
  libibus
  libmm-glib
  libnma-gtk4
  libpwquality
  modemmanager
  smbclient
  sound-theme-freedesktop
  udisks2
  upower)
makedepends=(
  docbook-xsl
  git
  meson
  python
)
checkdepends=(
  python-dbusmock
  python-gobject
  xorg-server-xvfb
)
optdepends=(
  'fwupd: device security panel'
  'gnome-remote-desktop: screen sharing'
  'gnome-user-share: WebDAV file sharing'
  'networkmanager: network settings'
  'openssh: remote login'
  'power-profiles-daemon: power profiles'
  'rygel: media sharing'
  'system-config-printer: printer settings'
)
provides=(gnome-control-center)
conflicts=(gnome-control-center)
_commit=ed5660b0d18dcba23b484f98a535cfaf6e4dcc4e  # tags/43.6^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-control-center.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        'pixmaps-dir.diff'
        # purism
        '0001-avatar-chooser-Adapt-to-work-on-librem5.patch'
        '0002-datetime-Fix-timezone-selection-map.patch'
        '0003-display-Request-a-smaller-size-for-arrangment-widget.patch'
        '0005-power-show-brightness-slider-in-a-separate-row.patch'
        '0006-sound-fix-test-dialog-on-small-screen.patch'
        'Add-patches-to-check-if-phone.patch'
        'network-connection-editor-Allow-dialog-to-resize.patch'
        '0002-UserAccount-fix-window-resizing-issue.patch'
        # Mobian patches
        '0004-power-add-more-suspend-timing-options.patch'
        'resize-connection-editor.patch'
        # Patches from upstream MRs
        '0001-removable-media-Wrap-GtkCheckButton-label-to-fit-nar.patch')
sha256sums=('SKIP'
            'SKIP'
            '8695bc08c06026b7bfdd43941b4e07bb3ffbaa4e709be0b23ee138d24b6dbc8f'
            '062e53654e8662ed53743b4c652b7b41c5c2993a713337626c832d0af3eda015'
            'a931eab101ac0e59b63df5bf21a797e05fec74399fdfe0ebb782e54bb3973a0b'
            'e02d1f097a29cf4963a4922aa3621344dc9fd68744e63fd17648356cde9c1936'
            'b02208d06a411e0e555ffa24613a654efebceeba48ae4d149e44dd1ea7fc77fc'
            'f7e874201febbdf4831f6ed65870167ba13da3fa6bb9eb11b2e2daac1b7b91c3'
            '09e36b0aa2c40d6b6f5be11fd9617223d5b142ff8b50db6fce0eeebc63007327'
            '5eeff09fb4bed46f959961ff8114269c0d0ac7dc823b83c870f68299420e5a87'
            '3b8e89665d5baa42fdc19921f1c299ddb012605669ac577c6e27a909a9eb3295'
            '3fedcc618dad7b6adc3955477a3bba95079da66a20a5964d84cc38b6ebb9cdd7'
            '42fd28f93d73950f5274688e70b0a37e6fbc3b2b1848d6156ca4ea5bf2f3811e'
            '4542cff2d86a234395e0aae485fa045c3fd0f904d406a29b3fb50e78dfd639ee')

pkgver() {
  cd ${_pkgname}
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ${_pkgname}

  # Install bare logos into pixmaps, not icons
  git apply -3 ../pixmaps-dir.diff

  git submodule init subprojects/gvc
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git -c protocol.file.allow=always submodule update

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  local meson_options=(
    -D documentation=true
  )

  arch-meson ${_pkgname} build "${meson_options[@]}"
  meson compile -C build
}

check() {
  GTK_A11Y=none xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  echo "X-Purism-FormFactor=Workstation;Mobile;" >> "$pkgdir/usr/share/applications/gnome-control-center.desktop"
}
