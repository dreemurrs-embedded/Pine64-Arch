# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar-mobile
_pkgname=gnome-calendar
pkgver=41.2
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64 armv7h aarch64)
license=(GPL)
depends=(evolution-data-server gsettings-desktop-schemas gnome-control-center
         libdazzle libhandy)
makedepends=(git meson)
optdepends=('evolution: ICS file import')
provides=('gnome-calendar')
conflicts=('gnome-calendar')
_commit=deb9ff18302457b91fb8013f98880c8e3a548c19  # tags/41.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#commit=$_commit"
        # Purism patches for adaptive UI
        0001-Add-calendar-today-symbolic.patch
        0002-window-Move-the-today-button-towards-the-center.patch
        0003-window-Make-the-Today-button-adaptive.patch
        0004-window-Strictly-center-the-view-switcher.patch
        0005-window-Relocate-the-view-switcher-to-the-bottom-when.patch
        0006-event-widget-Wrap-by-word.patch
        0007-event-editor-dialog-Use-HdyPreferencesGroup-and-HdyP.patch
        0008-event-editor-dialog-Better-style-sections.patch
        0009-appdata-Declare-the-supported-controls-and-displays.patch
        0010-window-Reduce-the-minimum-height.patch
        0011-data-Add-form-factors-to-desktop-file-to-mark-it-as-as-ad.patch
        0012-search-button-Make-the-results-fit-phones.patch
        0013-calendar-popover-Ensure-it-fits-phones.patch
        0014-utils-Add-gcal_get_abbreviated_month_name.patch
        0015-week-header-Show-abbreviated-month-names-if-they-don.patch
        0016-week-header-Ellipsize-the-Other-events-label.patch
        0017-event-widget-Reduce-the-slanted-variant-width-a-bit.patch
        0018-event-widget-Drop-even-the-ellipsis-if-it-doesn-t-fi.patch
        0019-calendar-management-dialog-Lower-the-width-request.patch
        0020-calendars-page-Make-the-rows-ellipsize.patch
        0021-window-Hide-Keyboard-Shortcuts-when-narrow.patch
        0022-event-popover-Reduce-the-text-s-width.patch
        # Mobian patches for fixing build
        0001-build-fix-build-with-meson-0.60.0.patch
        0002-quick-add-popover-reduce-width-to-fit-phone-screens.patch)
sha256sums=('SKIP'
            '565e459ca8e41befc34b56fa50cb30ce58867903461ba335144b31f2387f85b9'
            '8a0cb41d8a04ba5e962efbd27bb0054a73a03902316305907124183d71e8ebbf'
            'a6c5ddaa61a868f10774a8302a7a43654f7021b2bbe303c35a2a63715e9dfc03'
            'c41519054a4791f9e6a6d18bdf2a86e5b8ac47b997b2f7c1cac03ab4c6c3c9f4'
            'fa0ea2ec45329578d4d96e1a94c4dbb5809376375cd51865ce63e1288fc8e3a9'
            '65b0b6018c5d753e448b198fe7470c24774ba23ba051bde63678f7f7d73097cb'
            '44632e43e1477f7f37a510418b27c3471547a2247c69a16a0d898fb2a8c051df'
            '3d6d44f958e58f7086ee4fcc5658fa23dc94d829c1f7ff1e5e9cafea00028ed8'
            'c49a9140edc1d8a9e5d7bd83ce9c121820288f629d0794f13bd26e9c643776c9'
            'e878d3957d191191ae6a13dde6e55171a5c6dcb7a61a0c9b6b8d0826023fbf1b'
            '4af176a75d05f783b31aae63eedfa581c1cc3f4f1d6b34989d3cb7871dcefbc9'
            '2eedee1296878f7faa37efa9844be0549a484d20411de81bb2f28e16037096cf'
            '9251703684cffc7b2df765cc82cb27129252d68edec34b0bb01c8359293b0f2f'
            '1d8394b5e3e2c0080b341e6fdfba805a0c8ae6135f74f21cc80a6344a98f1b2e'
            'f0a34e74fd68da5caa7e1c577abd23deb789ccc4e60a42c44fb90fbfea6e993f'
            'cf7628dd10078391ad5e35ebf8793fef3487d60f3ae16408b542744abfb55a2e'
            'ae52ce52d052da99cb0796c200c4d25919751a77689e66ff99724bdcf3c586a9'
            'b6e4a96269fe20900a93556608f23ac0f838b46e67cdf6c70c12ed8e1c6fd862'
            'a6e2120cee70a9c94ea847e47e5a0e895c2d5d5464213f1f5e7d2ddcabe9959e'
            '7762be90edcec9f7ae7c961bb9f40fafcd22b8c3656bcb33168953d6978c6418'
            'a9d38e8c0347f658de5fa2bb5ab8617ee642d02dc28f3f08f3058b27698bea21'
            'a1ed69000ea562785932b62bebcebd53acb0912936ea02a5824fdf9337574228'
            'ba3e91a5bdd18e9e6b24eba1347949975ecaa1e3aa2ad29cc962500b0c06d899'
            '4a619f38725863a0a9cf30f102a8e7234773acd99d994fdc170c0743f6df9e95')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
