# Contributor Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Danct12 <danct12@disroot.org>

pkgname=gtk3-mobile
pkgver=3.24.30+62+g8d04980f38
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (Built with Purism patches)"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://www.gtk.org/"
depends=(atk cairo libxcursor libxinerama libxrandr libxi libepoxy gdk-pixbuf2
         dconf libxcomposite libxdamage pango shared-mime-info at-spi2-atk
         wayland libxkbcommon adwaita-icon-theme json-glib librsvg
         desktop-file-utils mesa cantarell-fonts libcolord rest libcups
         libcanberra fribidi iso-codes libcloudproviders tracker3
         gtk-update-icon-cache)
makedepends=(gobject-introspection gtk-doc git glib2-docs sassc meson
             wayland-protocols)
license=(LGPL)
_commit=8d04980f38d58bea7ba721a6ff2e3d38dfdc0486  # gtk-3-24
source=("git+https://gitlab.gnome.org/GNOME/gtk.git#commit=$_commit"
        gtk-query-immodules-3.0.hook
        # Purism patches
        librem5-Drop-reftests.patch
        Add-GtkHdyViewSwitcherButton.patch
        Add-GtkHdyViewSwitcher.patch
        Add-GtkHdyViewSwitcherBar.patch
        Add-GtkHdyAnimation.patch
        Add-GtkHdySqueezer.patch
        Add-GtkHdyViewSwitcherTitle.patch
        Add-GtkHdyShadowHelper.patch
        Add-GtkHdyNavigationDirection.patch
        Add-GtkHdySwipeable-and-GtkHdySwipeTracker.patch
        Add-GtkHdyClamp.patch
        Add-GtkHdyFlap.patch
        theme-Add-libhandy-styles.patch
        Add-padding-for-HdyViewSwitcherTitle.patch
        hdy-flap-Use-natural-size-for-folding-instead-of-minimum.patch
        Add-org.gtk.Settings.Purism.patch
        gtkprivate-Add-an-API-to-check-if-phone.patch
        aboutdialog-Port-to-phones.patch
        Add-the-view-sidebar-symbolic-icon.patch
        Port-file-chooser-to-phones.patch
        messagedialog-Set-orientation-based-on-device.patch
        dialog-Maximize-resizable-dialogs-on-phones.patch
        window-Maximize-resizable-pseudo-dialogs-on-mobile.patch
        headerbar-Use-a-back-button-in-dialogs-on-mobile.patch
        infobar-Move-the-action-area-below-on-the-phone.patch
        Reduce-the-font-chooser-minimum-size.patch
        printunixdialog-Adapt-for-phones.patch
        window-Disable-window-dragging-on-phones.patch
        librem5-Make-GtkShortcutsWindow-adaptive.patch
        scrolledwindow-Set-deceleration-value-based-on-the-device.patch
        events-Compress-touch-update-events.patch)
sha256sums=('SKIP'
            'de46e5514ff39a7a65e01e485e874775ab1c0ad20b8e94ada43f4a6af1370845'
            'eb23fd4fb6fdf355b3d9c8502772eb5bafc5a6c498de37b994d26354153d0c36'
            'e4bf5a6d4a4d9841241d4dd30b0bfa81d93aa1f107392b1dbb3beee7e2fe0a5d'
            '223d6f9411882754fce173a24f481c5b3b73c252cd61bf5c0cec76858042ea9e'
            'd82cd38b32a7389a11f0afaa998d79b2005638587f194b4098a1ad091579b418'
            '35bdee4c4d947fa5ebfc751565a61cff77892defb1d47e23a114b971f2801fa3'
            'c7abe65030476ff937bf1cae92d20e3b687272bf7912105506f84370e577fa5b'
            'c0d407ec37f78e54e66742bef569161d94186050c3b1554bdff1e8ff818073d0'
            'b4b4359bcc058cda1ee3aa1fb6cfd7b7b60bb3791ceb5832fb03bb20ff860298'
            'ae6f2df77cb50dbec30780a8550df33685e115113185190503ed7f9a2c798524'
            '6ce74ea7d90ffa33e4feec4c8436adaffd10c5bb1b51eafc76a95a84b3eb0e3a'
            '0d0a535702c1097e130c1663e60140f37ca8d521f80e1f32b51db6b14619d382'
            'deb83fc619d98da1c5f7c0610469c067134d7192feabf2f98731f80dd190459f'
            'af35d2a2c47b8c91b3e39b4b04c3e5292fefa14d041514eb31a32af6dfd4226c'
            '8c2c46c698fc016f78e249d8f5e58cc9d05f2b30428545932317f01d4c1db060'
            'dd45a31922dd3f991510231db2b6bba177b6bba87391359cb3c0f952efe35e8d'
            '75db1095d96bce920888c5d66f4226581789a43b16f4552926281b78e840a939'
            'ff47029358fba059bb8ea7206638dbc5ea1162ea54177359348c14bb142dfc61'
            'f5a46311e324a21ba84378f7afcf84a6b63bf30863f87819082abaf69e07ffea'
            'f13faf9e642de8e2ec1119aa4c836734a745b72f17d3749d4bff8594506dc2e2'
            'e1b2e006f1aa32964301d3995947dc741402301402dfdf8cec0e28b08eb40914'
            '54c1d3819d0c42107921b5a3ed7bc02e63bf8acc40a3e11382d27de323079c86'
            '2a19167711aa4acf675150401072686e3d5e2045c3611ded748d33a3495bdbbc'
            '4298dbfb786d26162f3012b86975669a3538903eb8fc103c354385ca64dd0c15'
            '5a1bd1b7a6398f941a2c5a2f106f8e58ba94ff7f0dba8f96fb9e74551e821177'
            '1ddc7c6d1416acc7fd115791641340f7235f43cb8193458a5b4f5ad7d6041f69'
            '2e6b997431443c53449af401bc5d4a3645c55ba3268809cf5c28cdfbe98e379c'
            'f81d536f84b589fa2bdf516c7f7a07083da5a0729467203a918fb8f715b366a7'
            '680a7de058d00f49ddac543392151502e9daca8f72a5ffd00543c4e93a9b6311'
            '7b6d71ac78967c7bcf45c3f9b458874373dcf5ac7b8df451df9dbe7d42e07a6d'
            '5b9e000106dbe67a8678c25ef70f8afc74edb8b348ce21445109539afdcfc486'
            '7dc4f66d115dec74bf5affae180e5de5e299b47410b2532473b3fb8a34aee709')

pkgver() {
  cd gtk
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtk

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  local meson_options=(
    -D broadway_backend=true
    -D cloudproviders=true
    -D tracker3=true
    -D colord=yes
    -D gtk_doc=true
    -D man=true
  )

  arch-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  provides=(gtk3 gtk3-print-backends libgtk-3.so libgdk-3.so libgailutil-3.so)
  conflicts=(gtk3 gtk3-print-backends)
  install=gtk3.install

  meson install -C build --destdir "$pkgdir"

  install -Dm644 /dev/stdin "$pkgdir/usr/share/gtk-3.0/settings.ini" <<END
[Settings]
gtk-icon-theme-name = Adwaita
gtk-theme-name = Adwaita
gtk-font-name = Cantarell 11
END

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 gtk-query-immodules-3.0.hook

  cd "$pkgdir"

  rm usr/bin/gtk-update-icon-cache
  rm usr/share/man/man1/gtk-update-icon-cache.1

  # provided by gtk3-docs
  rm -r usr/share/gtk-doc

  # provided by gtk3-demos
  rm -r usr/bin/gtk3-{demo,demo-application,icon-browser,widget-factory}
  rm -r usr/share/applications/gtk3-{demo,icon-browser,widget-factory}.desktop
  rm -r usr/share/glib-2.0/schemas/org.gtk.Demo.gschema.xml
  rm -r usr/share/icons/hicolor/*/apps/gtk3-{demo,widget-factory}[-.]*
  rm -r usr/share/man/man1/gtk3-{demo,demo-application,icon-browser,widget-factory}.1
}

# vim:set ts=2 sw=2 et:
