# Maintainer: Danct12 <danct12@disroot.org>
pkgname=chatty
pkgver=0.6.7
pkgrel=1
pkgdesc="Messaging client"
url="https://source.puri.sm/Librem5/chatty"
license=('GPL')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('evolution-data-server' 'feedbackd' 'gtk4' 'libhandy' 'libolm' 'libpurple'
    'gspell' 'libmm-glib' 'gnome-desktop')
makedepends=('meson' 'ninja' 'git' 'itstool')
checkdepends=('xorg-server-xvfb')
optdepends=('mmsd-tng: MMS support'
            'libpurple-lurch: OMEMO encryption'
            'libpurple-carbons: message carbons support'
            'libpurple-xmpp-http-upload: XMPP HTTP File Upload support'
            'purple-telegram: Telegram chat protocol support')
_commit="e8a1cbf339aeee55a3da2864c8880603fa940060" # tags/v0.6.7
source=(git+https://source.puri.sm/Librem5/chatty.git#commit=${_commit}
        '1099.patch')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd $pkgname
    git submodule update --init

    # https://source.puri.sm/Librem5/chatty/-/merge_requests/1099
    # tests fail to run without this patch
    patch -p1 < ../1099.patch
}

build() {
    arch-meson $pkgname output
    ninja -C output
}

check() {
    xvfb-run ninja -C output test
}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}
md5sums=('SKIP'
         '6daff1aa3b8821583251b7b646ab07e6')
