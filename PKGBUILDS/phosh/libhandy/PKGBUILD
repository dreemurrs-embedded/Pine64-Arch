# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

# DANCTNIX: Danct12 <danct12@disroot.org>
#  - Upgrade to 1.1.90

pkgname=libhandy
pkgver=1.1.90
pkgrel=1
pkgdesc="Library full of GTK+ widgets for mobile phones"
url="https://gitlab.gnome.org/GNOME/libhandy"
arch=(x86_64 armv7h aarch64)
license=(LGPL)
depends=(glib2 gtk3)
makedepends=(git gobject-introspection meson vala glade)
checkdepends=(xorg-server-xvfb)
provides=(libhandy-1.so)
_commit=89a1610b62533c9d6c5267cfac0108e724df25ba  # tags/1.1.90^0
source=("git+https://gitlab.gnome.org/GNOME/libhandy.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd libhandy
}

pkgver() {
  cd libhandy
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson libhandy build \
    -D gtk_doc=true \
    -D examples=false
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
