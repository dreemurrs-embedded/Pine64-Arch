# Maintainer: Danct12 <danct12@disroot.org>
pkgname=feedbackd
pkgver=0.2.1
pkgrel=1
pkgdesc="A daemon to provide haptic (and later more) feedback on events"
url="https://source.puri.sm/Librem5/feedbackd"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL')
depends=('gobject-introspection' 'gsound' 'json-glib' 'libgudev')
makedepends=('meson' 'vala' 'git')
_commit="1104cef50acb0b18a0d545e6a09773e3feb42e5b" # tags/v0.2.1
_fbdthemes_commit="d11a4c3e70f58345d8ab34cb75c3d5edfb5fe2be" # tags/v0.1.0
source=("feedbackd::git+https://source.puri.sm/Librem5/feedbackd.git#commit=${_commit}"
        "feedbackd-device-themes::git+https://source.puri.sm/Librem5/feedbackd-device-themes.git#commit=${_fbdthemes_commit}")

pkgver() {
  cd ${pkgname}
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  git submodule update --init
}

build() {
  arch-meson ${pkgname} output
  ninja -C output
}

check() {
  ninja -C output test
}

package() {
  DESTDIR="$pkgdir" ninja -C output install
  install -Dm644 "$srcdir"/${pkgname}/debian/feedbackd.udev \
    "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules
  sed -i 's/libexec/lib/g' "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules

  # FIXME: We aren't supposed to abuse video group, but we need to find a
  #        efficient way to add user to feedbackd group.
  sed -i 's/-G feedbackd/-G video/g' "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules

  # It would make much more sense to bundle fbd device configuration with the pkg.
  find ${srcdir}/feedbackd-device-themes/data -name \*.json \
    -exec cp {} ${pkgdir}/usr/share/feedbackd/themes \;
}

md5sums=('SKIP'
         'SKIP')
