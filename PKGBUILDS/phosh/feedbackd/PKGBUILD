# Maintainer: Danct12 <danct12@disroot.org>
pkgname=feedbackd
pkgver=0.0.0+git20220208
pkgrel=3
pkgdesc="A daemon to provide haptic (and later more) feedback on events"
url="https://source.puri.sm/Librem5/feedbackd"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL')
depends=('gobject-introspection' 'gsound' 'json-glib' 'libgudev')
makedepends=('meson' 'vala')
_fbdthemes_commit="68e029639e11a98c26949ffe87bcd6a025de1c55" # branch/master
source=(https://source.puri.sm/Librem5/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz
        https://source.puri.sm/Librem5/feedbackd-device-themes/-/archive/${_fbdthemes_commit}/feedbackd-device-themes-${_fbdthemes_commit}.tar.gz
        Have-alarms-ring-in-quiet-and-silent-profiles.patch)

prepare() {
  cd feedbackd-device-themes-${_fbdthemes_commit}
  patch -p1 < ../Have-alarms-ring-in-quiet-and-silent-profiles.patch
}

build() {
  arch-meson ${pkgname}-v${pkgver} output
  ninja -C output
}

package() {
  DESTDIR="$pkgdir" ninja -C output install
  install -Dm644 "$srcdir"/${pkgname}-v${pkgver}/debian/feedbackd.udev \
    "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules
  sed -i 's/libexec/lib/g' "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules

  # FIXME: We aren't supposed to abuse video group, but we need to find a
  #        efficient way to add user to feedbackd group.
  sed -i 's/-G feedbackd/-G video/g' "$pkgdir"/usr/lib/udev/rules.d/90-feedbackd.rules

  # It would make much more sense to bundle fbd device configuration with the pkg.
  find ${srcdir}/feedbackd-device-themes-${_fbdthemes_commit}/data -name \*.json \
    -exec cp {} ${pkgdir}/usr/share/feedbackd/themes \;
}

md5sums=('0b909fad76e7d9b4c13b9247a66f3ef0'
         '37a27fdb86916b104487cbf41ab7b52e'
         'eea9cc891b5f7da9110866adbb513c56')
