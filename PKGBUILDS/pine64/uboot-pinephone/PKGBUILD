# Maintainer: Danct12 <danct12@disroot.org>
# TODO: Find a way to build crust inside this PKGBUILD, right now it is prebuilt.

buildarch=8

pkgname=uboot-pinephone
pkgver=2022.01
pkgrel=5
pkgdesc="U-Boot for PinePhone"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
depends=('uboot-tools') # we need this for mkscr
makedepends=('bc' 'python' 'swig' 'dtc' 'python-setuptools')
install=${pkgname}.install
backup=("boot/boot.txt")

_commit_atf="d005bfcdff423fc84f03c657e0dcdb4b45621b22"

source=("https://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "arm-trusted-firmware-$_commit_atf.tar.gz::https://github.com/crust-firmware/arm-trusted-firmware/archive/${_commit_atf}.tar.gz"
        '0001-ATF-set-fno-stack-protector.patch'
        '0001-expose-DRAM-clock-speed.patch'
        'Improve-Allwinner-A64-timer-workaround.patch'
        'Add-volume_key-environment-variable.patch'
        'boot.txt'
        'mkscr'
        'update-u-boot'
        'scp.bin')
md5sums=('49ba74a214d14c6340c8067bc1ed5fa0'
         '0724b567a680e98d3240648ea14183ba'
         '348a6943a1c220047ce5312b59da95b6'
         '061a510280d16bddf16679cf0d23bf6e'
         '98ad5f31b1d7ff2e6e03555f29ebc6e6'
         'fb9def68f0b44c69f88f7923e21379cc'
         '06c27a80909035513aac45036e8d954b'
         '021623a04afd29ac3f368977140cfbfd'
         '5f85361d1d5ce47ff11ec4009cc2e18a'
         '90616d501a12d6f7a12c4349f789c343')

prepare() {
  cd arm-trusted-firmware-${_commit_atf}
  patch -N -p1 < ../0001-ATF-set-fno-stack-protector.patch

  cd ../u-boot-${pkgver/rc/-rc}
  patch -N -p1 < ../0001-expose-DRAM-clock-speed.patch
  patch -N -p1 < ../Improve-Allwinner-A64-timer-workaround.patch
  patch -N -p1 < ../Add-volume_key-environment-variable.patch

  cp ../scp.bin .
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd arm-trusted-firmware-${_commit_atf}

  make PLAT=sun50i_a64 DEBUG=1 bl31
  cp build/sun50i_a64/debug/bl31.bin ../u-boot-${pkgver/rc/-rc}

  cd ../u-boot-${pkgver/rc/-rc}

  # This builds the PineTab (624)
  msg2 "Building U-Boot for PineTab..."
  make distclean
  make pinetab_defconfig
  echo 'CONFIG_IDENT_STRING=" DanctNIX ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  echo 'CONFIG_DRAM_CLK=624' >> .config
  echo 'CONFIG_BOOTDELAY=0' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinetab-624.bin

  # Now we build PineTab (552)
  echo 'CONFIG_DRAM_CLK=552' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinetab-552.bin

  # Now we build PineTab (528)
  echo 'CONFIG_DRAM_CLK=528' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinetab-528.bin

  # Now we build PineTab (492)
  echo 'CONFIG_DRAM_CLK=492' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinetab-492.bin

  # This builds the PinePhone (624)
  msg2 "Building U-Boot for PinePhone..."
  make pinephone_defconfig
  echo 'CONFIG_IDENT_STRING=" DanctNIX ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  echo 'CONFIG_DRAM_CLK=624' >> .config
  echo 'CONFIG_BOOTDELAY=0' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinephone-624.bin

  # Now we build PinePhone (552)
  echo 'CONFIG_DRAM_CLK=552' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinephone-552.bin

  # Now we build PinePhone (528)
  echo 'CONFIG_DRAM_CLK=528' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinephone-528.bin

  # Now we build PinePhone (492)
  echo 'CONFIG_DRAM_CLK=492' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinephone-492.bin
}

package() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}"/boot

  cp u-boot-sunxi-with-spl-pinetab{-624,-552,-528,-492}.bin "${pkgdir}"/boot
  cp u-boot-sunxi-with-spl-pinephone{-624,-552,-528,-492}.bin "${pkgdir}"/boot

  cp ../boot.txt ../mkscr "${pkgdir}"/boot
  mkdir -p "${pkgdir}"/usr/bin && cp ../update-u-boot "${pkgdir}"/usr/bin
}
