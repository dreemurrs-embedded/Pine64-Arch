# Maintainer: Danct12 <danct12@disroot.org>
# TODO: Find a way to build crust inside this PKGBUILD, right now it is prebuilt.

buildarch=8

pkgname=uboot-pinephone
pkgver=v2020.10_rc3
pkgrel=2
pkgdesc="U-Boot for Pine64 - CRUST EDITION"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
makedepends=('bc' 'python' 'swig' 'dtc')
install=${pkgname}.install

_commit_uboot="7206996ef7f89375dd74b275ced62d85f8bc7f42"
_commit_atf="f928897fa5df6afa0f70bb1b1011e4bb03cb4596"

source=("u-boot-$_commit_uboot.tar.gz::https://gitlab.com/pine64-org/u-boot/-/archive/${_commit_uboot}/u-boot-${_commit_uboot}.tar.gz"
        "arm-trusted-firmware-$_commit_atf.tar.gz::https://github.com/crust-firmware/arm-trusted-firmware/archive/${_commit_atf}.tar.gz"
        '0001-ATF-set-fno-stack-protector.patch'
        '0001-configs-Add-Pinetab-configuration.patch'
        'boot.txt'
        'mkscr'
        'scp.bin')
md5sums=('96ca5597c63d724b245315ca88589a21'
         'ec70813ae71f978f07795cde13ba42c0'
         '348a6943a1c220047ce5312b59da95b6'
         'bdb8e9686df7fccbedf8577a8d3e350a'
         'e172791a0705f1be46a1191235bfedac'
         '021623a04afd29ac3f368977140cfbfd'
         'af8268d4f7c026d375e60044fdf12691')

prepare() {
  cd arm-trusted-firmware-${_commit_atf}
  patch -N -p1 < ../0001-ATF-set-fno-stack-protector.patch

  cd ../u-boot-${_commit_uboot}
  patch -N -p1 < ../0001-configs-Add-Pinetab-configuration.patch

  cp ../scp.bin .
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd arm-trusted-firmware-${_commit_atf}

  make PLAT=sun50i_a64 DEBUG=1 bl31
  cp build/sun50i_a64/debug/bl31.bin ../u-boot-${_commit_uboot}

  cd ../u-boot-${_commit_uboot}

  # This builds the PineTab (624)
  echo "Building U-Boot for PineTab..."
  make distclean
  make pinetab_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_CMD_BTRFS=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinetab.bin
  mv u-boot-sunxi-with-spl-pinetab.bin u-boot-sunxi-with-spl-pinetab-624.bin

  # Now we build PineTab (552)
  echo 'CONFIG_DRAM_CLK=552' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinetab.bin
  mv u-boot-sunxi-with-spl-pinetab.bin u-boot-sunxi-with-spl-pinetab-552.bin

  # Now we build PineTab (492)
  echo 'CONFIG_DRAM_CLK=492' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinetab.bin
  mv u-boot-sunxi-with-spl-pinetab.bin u-boot-sunxi-with-spl-pinetab-492.bin

  # This builds the PinePhone (624)
  echo "Building U-Boot for PinePhone..."
  make pinephone_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_CMD_BTRFS=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  echo 'CONFIG_DRAM_CLK=624' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinephone.bin
  mv u-boot-sunxi-with-spl-pinephone.bin u-boot-sunxi-with-spl-pinephone-624.bin

  # Now we build PinePhone (552)
  echo 'CONFIG_DRAM_CLK=552' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinephone.bin
  mv u-boot-sunxi-with-spl-pinephone.bin u-boot-sunxi-with-spl-pinephone-552.bin

  # Now we build PinePhone (492)
  echo 'CONFIG_DRAM_CLK=492' >> .config
  make EXTRAVERSION=-${pkgrel}
  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl-pinephone.bin
  mv u-boot-sunxi-with-spl-pinephone.bin u-boot-sunxi-with-spl-pinephone-492.bin
}

package() {
  cd u-boot-${_commit_uboot}

  mkdir -p "${pkgdir}"/boot

  cp u-boot-sunxi-with-spl-pinetab{-624,-552,-492}.bin "${pkgdir}"/boot
  cp u-boot-sunxi-with-spl-pinephone{-624,-552,-492}.bin "${pkgdir}"/boot

  cp ../boot.txt ../mkscr "${pkgdir}"/boot
  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d "${pkgdir}"/boot/boot.txt "${pkgdir}"/boot/boot.scr
}
