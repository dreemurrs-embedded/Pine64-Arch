# Maintainer: Danct12 <danct12@disroot.org>
# TODO: Find a way to build crust inside this PKGBUILD, right now it is prebuilt.

buildarch=8

pkgname=uboot-pinephone
pkgver=v2021.07_rc4
pkgrel=2
pkgdesc="U-Boot for PinePhone (contains crust scp firmware)"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
depends=('uboot-tools') # we need this for mkscr
makedepends=('bc' 'python' 'swig' 'dtc' 'python-setuptools')
install=${pkgname}.install
backup=("boot/boot.txt")

_commit_uboot="89cb280f35abc0f2b0b66ddec035d1ebf1674e64"
_commit_atf="fb6953b28903d6ba4b097c19ee90ea75f5aa4e74"

source=("u-boot-$_commit_uboot.tar.gz::https://gitlab.com/pine64-org/u-boot/-/archive/${_commit_uboot}/u-boot-${_commit_uboot}.tar.gz"
        "arm-trusted-firmware-$_commit_atf.tar.gz::https://github.com/crust-firmware/arm-trusted-firmware/archive/${_commit_atf}.tar.gz"
        '0001-ATF-set-fno-stack-protector.patch'
        '0001-configs-Add-Pinetab-configuration.patch'
        '0001-expose-DRAM-clock-speed.patch'
        'Improve-Allwinner-A64-timer-workaround.patch'
        'boot.txt'
        'mkscr'
        'scp.bin')
md5sums=('470606502bdfd4c1f1492d8f0fc451a1'
         '4c1beabd0d926920f5c8de0b521b6d93'
         '348a6943a1c220047ce5312b59da95b6'
         'bdb8e9686df7fccbedf8577a8d3e350a'
         '061a510280d16bddf16679cf0d23bf6e'
         '98ad5f31b1d7ff2e6e03555f29ebc6e6'
         '90caaaf7c972240e2992c5c46102b2e9'
         '021623a04afd29ac3f368977140cfbfd'
         '90616d501a12d6f7a12c4349f789c343')

prepare() {
  cd arm-trusted-firmware-${_commit_atf}
  patch -N -p1 < ../0001-ATF-set-fno-stack-protector.patch

  cd ../u-boot-${_commit_uboot}
  patch -N -p1 < ../0001-configs-Add-Pinetab-configuration.patch
  patch -N -p1 < ../0001-expose-DRAM-clock-speed.patch
  patch -N -p1 < ../Improve-Allwinner-A64-timer-workaround.patch

  cp ../scp.bin .
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd arm-trusted-firmware-${_commit_atf}

  make PLAT=sun50i_a64 DEBUG=1 bl31
  cp build/sun50i_a64/debug/bl31.bin ../u-boot-${_commit_uboot}

  cd ../u-boot-${_commit_uboot}

  # This builds the PineTab (624)
  msg2 "Building U-Boot for PineTab..."
  make distclean
  make pinetab_defconfig
  echo 'CONFIG_IDENT_STRING=" DanctNIX ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinetab-624.bin

  # Now we build PineTab (552)
  echo 'CONFIG_DRAM_CLK=552' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinetab-552.bin

  # Now we build PineTab (528)
  echo 'CONFIG_DRAM_CLK=528' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinetab-528.bin

  # Now we build PineTab (492)
  echo 'CONFIG_DRAM_CLK=492' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinetab-492.bin

  # This builds the PinePhone (624)
  msg2 "Building U-Boot for PinePhone..."
  make pinephone_defconfig
  echo 'CONFIG_IDENT_STRING=" DanctNIX ARM"' >> .config
  echo 'CONFIG_SERIAL_PRESENT=y' >> .config
  echo 'CONFIG_GZIP=y' >> .config
  echo 'CONFIG_CMD_UNZIP=y' >> .config
  echo 'CONFIG_CMD_EXT4=y' >> .config
  echo 'CONFIG_SUPPORT_RAW_INITRD=y' >> .config
  echo 'CONFIG_CMD_EXT4_WRITE=n' >> .config
  echo 'CONFIG_EXT4_WRITE=n' >> .config
  echo 'CONFIG_DRAM_CLK=624' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinephone-624.bin

  # Now we build PinePhone (552)
  echo 'CONFIG_DRAM_CLK=552' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinephone-552.bin

  # Now we build PinePhone (528)
  echo 'CONFIG_DRAM_CLK=528' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinephone-528.bin

  # Now we build PinePhone (492)
  echo 'CONFIG_DRAM_CLK=492' >> .config
  make EXTRAVERSION=-${pkgrel}
  mv u-boot-sunxi-with-spl.bin u-boot-sunxi-with-spl-pinephone-492.bin
}

package() {
  cd u-boot-${_commit_uboot}

  mkdir -p "${pkgdir}"/boot

  cp u-boot-sunxi-with-spl-pinetab{-624,-552,-528,-492}.bin "${pkgdir}"/boot
  cp u-boot-sunxi-with-spl-pinephone{-624,-552,-528,-492}.bin "${pkgdir}"/boot

  cp ../boot.txt ../mkscr "${pkgdir}"/boot
}
