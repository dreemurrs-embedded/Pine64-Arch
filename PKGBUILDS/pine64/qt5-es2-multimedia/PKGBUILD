# Maintainer: Felix Yan 
# Contributor: Andrea Scarpino 

pkgname=qt5-es2-multimedia
_pkgname=qt5-multimedia
_qtver=5.15.0
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64' 'aarch64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for audio, video, radio and camera functionality'
depends=('qt5-es2-base' 'gst-plugins-base' 'libpulse' 'openal')
makedepends=('qt5-es2-declarative' 'gst-plugins-bad')
optdepends=('qt5-declarative: QML bindings' 'gst-plugins-good: camera support, additional plugins'
            'gst-plugins-bad: camera support, additional plugins' 'gst-plugins-ugly: additional plugins'
            'gst-libav: ffmpeg plugin')
groups=('qt' 'qt5')
provides=("qt5-multimedia=$pkgver")
conflicts=('qt5-multimedia')
_pkgfqn="${_pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('0708d867697f392dd3600c5c1c88f5c61b772a5250a4d059dca67b844af0fbd7')

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${_pkgname}
}
