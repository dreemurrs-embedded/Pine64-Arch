# Maintainer: Danct12 <danct12@disroot.org>

buildarch=8

pkgname=uboot-pinetab2
pkgver=2023.07.02
pkgrel=3
pkgdesc="U-Boot for PineTab 2"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
depends=('uboot-tools')
makedepends=('bc' 'git' 'python' 'python-pyelftools' 'python-setuptools' 'swig')
replaces=(uboot-pinetab2-mainline)
install=${pkgname}.install
backup=("boot/boot.txt")

_commit_uboot="81dac0348fb1d1d8279634d1438bf2a88b1ca9b7"
_commit_rkbin="b4558da0860ca48bf1a571dd33ccba580b9abe23"

source=("$pkgname::git+https://github.com/Kwiboo/u-boot-rockchip.git#commit=$_commit_uboot"
        "rkbin::git+https://github.com/rockchip-linux/rkbin.git#commit=$_commit_rkbin"
        'config'
        'boot.txt'
        'mkscr'
        'update-u-boot')
md5sums=('SKIP'
         'SKIP'
         'ff5c59970e70f67abbf19819c06efb37'
         'ed65ec486f0e0dc9e9c3118dffaa3782'
         '021623a04afd29ac3f368977140cfbfd'
         'ec7c2005cb5cc9a23d1eb0e1bf089bf9')

prepare() {
  cd ${pkgname}
  cp ${srcdir}/config .config
}

build() {
  cd ${pkgname}

  make EXTRAVERSION=-${pkgrel} \
    BL31=../rkbin/bin/rk35/rk3568_bl31_v1.43.elf \
    ROCKCHIP_TPL=../rkbin/bin/rk35/rk3566_ddr_1056MHz_v1.18.bin
}

package() {
  cd ${pkgname}

  mkdir -p "${pkgdir}/boot"

  cp ./idbloader.img "${pkgdir}/boot/idbloader.img"
  cp ./u-boot.itb "${pkgdir}/boot/u-boot.itb"

  cp ../{boot.txt,mkscr} "${pkgdir}"/boot
  mkdir -p "${pkgdir}"/usr/bin && cp ../update-u-boot "${pkgdir}"/usr/bin
}
