pkgname=brcm-firmware
pkgrel=2
pkgdesc="Firmware files for BCM43 chips"
makedepends=('git')
arch=('any')
url="https://xff.cz/git/linux-firmware"
license=('custom')
pkgver=20211027.6e8e591
options=(!strip)
depends=('linux-firmware')
source=("git+https://megous.com/git/linux-firmware")
md5sums=('SKIP')

pkgver() {
  # Mimics ABS pkgver described like this:
  # Commit date + git rev-parse --short origin/master
  cd "${srcdir}/linux-firmware"
  (
  git show --format='%cI' -q master | sed 's/T.*//g;s/-//g'
  echo .
  git rev-parse --short master
  ) | tr -d '\n'
}

package() {
  mkdir -p ${pkgdir}/usr/lib/firmware
  cp -rv ${srcdir}/linux-firmware/brcm ${pkgdir}/usr/lib/firmware
  rm -r ${pkgdir}/usr/lib/firmware/brcm/2020-02-12

  cd ${pkgdir}/usr/lib/firmware/brcm
  rm brcmfmac43362-sdio.bin brcmfmac43455-sdio.bin brcmfmac43455-sdio.clm_blob
}
# vim:set ts=2 sw=2 et
