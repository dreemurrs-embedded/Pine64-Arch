# U-Boot: Pinephone Pro based on PKGBUILD for RK3399
# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <furkan@fkardame.com>

pkgname=uboot-pinephonepro
pkgver=2022.01
pkgrel=8
_tfaver=2.6
pkgdesc="U-Boot for Pinephone Pro"
arch=('aarch64')
url='https://git.sr.ht/~martijnbraam/u-boot'
license=('GPL')
depends=('uboot-tools') # we need this for mkscr
makedepends=('git' 'arm-none-eabi-gcc' 'dtc' 'bc' 'python3')
provides=('uboot')
conflicts=('uboot')
install=${pkgname}.install
backup=("boot/boot.txt")
source=("https://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        "https://git.trustedfirmware.org/TF-A/trusted-firmware-a.git/snapshot/trusted-firmware-a-$_tfaver.tar.gz"
        'rockchip-Add-initial-support-for-the-PinePhone-Pro.patch'
        'pinephone-pro-remove-cargo-culted-iodomain-config.patch'
        'mkscr'
        'boot.txt')
sha256sums=('81b4543227db228c03f8a1bf5ddbc813b0bb8f6555ce46064ef721a6fc680413'
            '4e59f02ccb042d5d18c89c849701b96e6cf4b788709564405354b5d313d173f7'
            '7c3d76f4bee0e54900142043241248072e334387065212141e1f600afe0aafba'
            '8fda0085f428ca73f7a6a1d8f17fc7d5c25c4eaa6ea6f05b57fb61612bea021a'
            'a4fc8b6b92bc364d6542670d294aa618a8501fb8729f415cc0a3eed776ef0c8e'
            '6e618b53037e0319dcd5fc8ada4cbf342df59953c429fe0f1eebe4e26cb9d88d')

prepare() {
  cd u-boot-${pkgver/rc/-rc}
  patch -p1 -N < ../rockchip-Add-initial-support-for-the-PinePhone-Pro.patch
  patch -p1 -N < ../pinephone-pro-remove-cargo-culted-iodomain-config.patch

  # Change baudrate from 1.5mbps to 115200, it makes it Tow-Boot compliance. (hi samueldr)
  # https://github.com/Tow-Boot/Tow-Boot/blob/released/support/builders/tow-boot/default.nix
  echo ':: Patching baud rate'
  (PS4=" $ "
    for f in configs/*rk3399* configs/*rk3328*; do
      (set -x
      sed -i"" -e 's/CONFIG_BAUDRATE=1500000/CONFIG_BAUDRATE=115200/' "$f"
      )
    done
    for f in arch/arm/dts/*rk3399*.dts* arch/arm/dts/*rk3328*.dts*; do
      (set -x
      sed -i"" -e 's/serial2:1500000n8/serial2:115200n8/' "$f"
      )
    done
  )
}

build() {
  cd trusted-firmware-a-$_tfaver
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  make PLAT=rk3399
  cp build/rk3399/release/bl31/bl31.elf ../u-boot-${pkgver/rc/-rc}
  cd ../u-boot-${pkgver/rc/-rc}
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  make pinephone-pro-rk3399_defconfig
  echo 'CONFIG_IDENT_STRING=" DanctNIX ARM"' >> .config
  echo 'CONFIG_BOOTDELAY=0' >> .config

  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}/boot"
  cp idbloader.img u-boot.itb ../boot.txt ../mkscr  "${pkgdir}/boot/"
}

