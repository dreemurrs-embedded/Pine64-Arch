# Maintainer: Danct12 <danct12@disroot.org>
pkgname=device-pine64-pinephonepro
pkgver=0.1
pkgrel=1
pkgdesc="PinePhone Pro device package"
arch=(aarch64)
url="https://github.com/dreemurrs-embedded/Pine64-Arch"
license=('MIT')
depends=(
	danctnix-tweaks
	linux-megi
	uboot-pinephonepro
	eg25-manager
	)
install="$pkgname.install"
source=(10-proximity.rules
	10-pinephone-brightness.rules
	pinephone-suspend-hook.sh
	machine-info
	pulseaudio-pinetweaks.conf
	gsk-renderer-gl.sh)

package() {
	# Kanged from Mobian, this is to make iio-sensor-proxy works with proximity sensor.
	install -Dm644 "$srcdir"/10-proximity.rules "$pkgdir"/usr/lib/udev/rules.d/10-proximity.rules

	# Calibrate minimum brightness
	install -Dm644 "$srcdir"/10-pinephone-brightness.rules "$pkgdir"/usr/lib/udev/rules.d/10-pinephone-brightness.rules

	# Current workaround for suspend
	install -Dm755 "$srcdir"/pinephone-suspend-hook.sh "$pkgdir"/usr/lib/systemd/system-sleep/pinephone-suspend-hook.sh

	install -Dm644 "$srcdir"/machine-info "$pkgdir"/etc/machine-info

	install -Dm644 "$srcdir"/pulseaudio-pinetweaks.conf "$pkgdir"/etc/pulse/daemon.conf.d/pinetweaks.conf

	install -Dm755 "$srcdir"/gsk-renderer-gl.sh -t "$pkgdir"/etc/profile.d/
}

md5sums=('b72ff096c022588f72c226283ac328bd'
         '3a478fece4381cf41fb795009bfe8cc3'
         'b0912b95d7d810a0f161d9bd79fc162b'
         'a3414404885e67eadef3c22ef83034df'
         '1504e684e5a2aec439e0674cdfc9d3c1'
         'df623da01912f5ce89c178639fc22656')
