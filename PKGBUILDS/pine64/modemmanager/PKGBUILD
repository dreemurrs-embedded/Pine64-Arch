# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@archlinux.org>

pkgbase=modemmanager
pkgname=(modemmanager libmm-glib)
pkgver=1.20.4
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=(x86_64 armv7h aarch64)
url="https://www.freedesktop.org/wiki/Software/ModemManager/"
license=(GPL2 LGPL2.1)
depends=(systemd libgudev polkit ppp libqmi libmbim
         mobile-broadband-provider-info)
makedepends=(gtk-doc gobject-introspection vala meson git bash-completion)
checkdepends=(python-gobject python-dbus)
options=(debug)
_commit=6926459500fd927e7cceb589a9e4113d4edb04e6  # tags/1.20.4^0
source=("git+https://gitlab.freedesktop.org/mobile-broadband/ModemManager.git#commit=$_commit"
        'modemmanager-polkit.rules'
        '0001-mm-broadband-modem-improve-voice-capabilities-detect.patch')
sha256sums=('SKIP'
            '2255d33e53047943c9da6edd1e8b66397718d4bba1ead968708e4b6dd3101b98'
            '6272b09b59ba7a395f86502083ea2609a885ffa696c8acd20ed99cb18b6de92b')

pkgver() {
  cd ModemManager
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ModemManager

  # PinePhone Patches
  patch -p1 -N < ../0001-mm-broadband-modem-improve-voice-capabilities-detect.patch
}

build() {
  local meson_options=(
    -D dbus_policy_dir=/usr/share/dbus-1/system.d
    -D dist_version="\"$pkgver-$pkgrel\""
    -D gtk_doc=true
    -D plugin_qcom_soc=disabled
    -D polkit=permissive
    -D vapi=true
    -D at_command_via_dbus=true
  )

  arch-meson ModemManager build "${meson_options[@]}"
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_modemmanager() {
  depends+=(libmm-glib.so libg{lib,object,io,module}-2.0.so libsystemd.so libgudev-1.0.so
            libqmi-glib.so libmbim-glib.so)
  optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"
  _pick libmm usr/include
  _pick libmm usr/lib/girepository-1.0
  _pick libmm usr/lib/libmm-glib.so*
  _pick libmm usr/lib/pkgconfig
  _pick libmm usr/share/gir-1.0
  _pick libmm usr/share/gtk-doc/html/libmm-glib
  _pick libmm usr/share/vala

  install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"
  install -m644 -D "$srcdir/modemmanager-polkit.rules" \
        "$pkgdir/usr/share/polkit-1/rules.d/01-org.freedesktop.ModemManager.rules"

  # Disable suspend/resume hooks for the EG25-G modem in the PinePhone
  sed -i -e 's|bin/ModemManager|bin/ModemManager --test-quick-suspend-resume|g' \
        "$pkgdir/usr/lib/systemd/system/ModemManager.service"
}

package_libmm-glib() {
  pkgdesc="ModemManager library"
  depends=(libg{lib,object,io}-2.0.so)
  provides=(libmm-glib.so)

  mv libmm/* "$pkgdir"
}

# vim:set sw=2 et:
