# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Dan Johansen <strit@manjaro.org>

pkgname=plasma-nano
pkgver=5.23.0
pkgrel=1
pkgdesc="A minimal Plasma shell package"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://invent.kde.org/kde/plasma-nano"
license=('GPL2')
depends=('desktop-file-utils' 'plasma-workspace')
makedepends=('cmake' 'extra-cmake-modules')
source=("https://invent.kde.org/kde/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
install=$pkgname.install
md5sums=('83e635a316b6a2a06a0a35c68dce926b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-v$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
