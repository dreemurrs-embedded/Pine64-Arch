# Maintainer: Danct12 <danct12@disroot.org>
pkgname=hfd-service
pkgver=0.1.0
pkgrel=1
pkgdesc="Service that manages human feedback devices on mobile devices"
url="https://gitlab.com/ubports/core/hfd-service"
license=('LGPL')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('qt5-base' 'qt5-feedback')
makedepends=('cmake' 'cmake-extras')
source=(https://gitlab.com/ubports/core/hfd-service/-/archive/$pkgver/hfd-service-$pkgver.tar.gz
        plugin_install_dir.patch
        hfd-service.service)

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 -N < ../plugin_install_dir.patch
}

build() {
    cmake -B build -S "$pkgname-$pkgver" \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DENABLE_LIBHYBRIS=OFF
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build

    install -Dm644 hfd-service.service $pkgdir/usr/lib/systemd/system/hfd-service.service
    rm -rv $pkgdir/etc/init
}
md5sums=('00145353bc7f1e8bfb7b9508da71d7ce'
         'f65d8618960aa3466db99708abd974de'
         '8ec0965e57b65a1769e148cfb151628f')
