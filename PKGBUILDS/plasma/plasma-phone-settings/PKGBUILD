# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Bernhard Landauer <oberon@manjaro.org>

pkgname=plasma-phone-settings
pkgver=r75.648f779
pkgrel=1
arch=('any')
url="https://invent.kde.org/plasma-mobile/plasma-phone-settings"
license=('CC0-1.0')
pkgdesc='Configuration files for Plasma Mobile deployments'
makedepends=('git')
optdepends=('modemmanager: Telephony capabilities')
replaces=('plasma-mobile-settings')
install=$pkgname.install
source=("plasma-phone-settings::git+https://invent.kde.org/plasma-mobile/plasma-phone-settings.git")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

package() {
  cp -rv "$pkgname/etc" "$pkgdir/"
  cp -rv "$pkgname/usr" "$pkgdir/"

  # Enable Pipewire for app switcher thumbnails
  mkdir -p $pkgdir/usr/lib/systemd/user/default.target.wants
  ln -sfv /usr/lib/systemd/user/pipewire.service \
      $pkgdir/usr/lib/systemd/user/default.target.wants/pipewire.service

  # Enable hfd-service for vibrations
  mkdir -p $pkgdir/usr/lib/systemd/system/multi-user.target.wants
  ln -sfv /usr/lib/systemd/system/hfd-service.service \
      $pkgdir/usr/lib/systemd/system/multi-user.target.wants/hfd-service.service
}
