# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-mobile-nm
_pkgname=plasma-nm
pkgver=5.25.4
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections - Mobile Edition'
arch=(x86_64 armv7h aarch64)
url='https://plasma-mobile.org/'
license=(GPL2)
depends=(plasma-workspace modemmanager-qt networkmanager-qt qca-qt5 mobile-broadband-provider-info)
makedepends=(extra-cmake-modules openconnect)
optdepends=('openconnect: Cisco AnyConnect VPN plugin')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("https://download.kde.org/stable/plasma/$pkgver/$_pkgname-$pkgver.tar.xz")
sha256sums=('62621220cdd030098fa4c90a613cd279f8be29d21ff27d45792fcf7ac529f790')

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_MOBILE=True
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
