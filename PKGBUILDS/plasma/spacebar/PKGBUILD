# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Bernhard Landauer <bernhard@manjaro.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=spacebar
pkgver=22.06
pkgrel=1
pkgdesc="SMS and messaging application for Mobile Devices running Plasma"
arch=('x86_64' 'aarch64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('kcontacts'
    'knotifications'
    'kirigami2'
    'kpeople'
    'ki18n'
    'qt5-quickcontrols2'
    'modemmanager-qt'
    'telepathy-qt'
    'libphonenumber'
    'kdbusaddons'
    'kio')
makedepends=('cmake'
    'extra-cmake-modules'
    'qcoro-qt5')
source=("https://download.kde.org/stable/plasma-mobile/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('367e63ba393cb4e9f49ae60f261fb65b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../spacebar-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
