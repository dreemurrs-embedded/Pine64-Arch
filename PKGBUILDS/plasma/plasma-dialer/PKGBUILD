# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <tech@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-dialer
pkgver=24.08.0
pkgrel=2
pkgdesc="Phone Dialer for Plasma Mobile"
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=(callaudiod
         desktop-file-utils
         kcontacts
         kcoreaddons
         kdbusaddons
         ki18n
         kio
         kirigami-addons
         knotifications
         kpeople
         libphonenumber
         modemmanager-qt
         qtmpris)
makedepends=(cmake
             extra-cmake-modules
             kdoctools5
             plasma-wayland-protocols)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('133b5484197f6ce4d994f323dea5e5fb')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
