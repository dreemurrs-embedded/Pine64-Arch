# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <tech@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-dialer
pkgver=23.01.0
pkgrel=5
pkgdesc="Phone Dialer for Plasma Mobile"
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=(callaudiod
         desktop-file-utils
         kcontacts5
         kdbusaddons5
         kio5
         kirigami-addons5
         knotifications5
         kpeople5
         libphonenumber
         modemmanager-qt5
         pulseaudio-qt
         qt5-feedback
         qt5-quickcontrols2
         qtmpris
         telepathy-mission-control)
makedepends=(cmake
             extra-cmake-modules
             kdoctools5
             plasma-wayland-protocols)
source=("https://download.kde.org/stable/plasma-mobile/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('2c9795160dbe66c16510d02c0fb357d0')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
