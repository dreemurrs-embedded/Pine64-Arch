# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <tech@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-dialer
pkgver=22.11
pkgrel=1
pkgdesc="Phone Dialer for Plasma Mobile"
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('callaudiod' 'desktop-file-utils' 'kcontacts' 'kdbusaddons' 'kio' 'knotifications'
         'kpeople' 'libphonenumber' 'modemmanager-qt' 'pulseaudio-qt' 'qt5-feedback'
         'qt5-quickcontrols2' 'qtmpris' 'telepathy-mission-control' 'telepathy-qt'
         'kirigami-addons')
makedepends=('cmake' 'extra-cmake-modules' 'kdoctools' 'plasma-wayland-protocols')
source=("https://download.kde.org/stable/plasma-mobile/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('ea3d88fe4aaddfdb30102111d997ae1d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
