# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-phone-components
pkgver=5.23.4
pkgrel=2
pkgdesc="General UI components for Plasma Phone including shell, containment and applets."
arch=('aarch64' 'x86_64')
url="https://community.kde.org/Plasma/Mobile"
license=('GPL3')
depends=('desktop-file-utils' 'plasma-workspace' 'modemmanager-qt' 'kpeople' 'telepathy-qt' 'libphonenumber'
         'plasma-wayland-session' 'pipewire-media-session')
makedepends=('git' 'cmake' 'baloo' 'extra-cmake-modules' 'kdoctools')
source=("https://invent.kde.org/plasma/plasma-phone-components/-/archive/v${pkgver}/plasma-phone-components-v${pkgver}.tar.gz")
install=$pkgname.install
sha512sums=('721965d2072ff155fccd9efbaeed55ce0366ee93c1bdc21c6e9e08caa1fbee82141cfc993a96bbeb1cbc042ea026a9376716c864ae995fcfa8698f7ac18f05f0')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-v${pkgver} \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
